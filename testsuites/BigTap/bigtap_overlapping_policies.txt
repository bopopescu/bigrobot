*Settings
Documentation  Release:Blackbird
Suite Setup  overlap suite setup
Suite Teardown   overlap suite teardown
Test Setup   base test setup
Test Teardown  base test teardown 
Force Tags   platform Sanity
Library  keywords/BsnCommon.py
Library  keywords/AppController.py
Library  keywords/BigTap.py
Library  keywords/SwitchLight.py
Library  Collections.py

* Variable
${switch_1_alias}  AF1
${switchlight_version}  SwitchLight 2.0.0
${netmask}  18
${gateway}  10.192.64.1
${dns_server}  10.192.3.1
${dns_domain}  bigswitch.com
${filter_1}  ethernet1  
${filter_nick_1}  F1
${filter_2}  ethernet1  
${filter_nick_2}  F2
${delivery_1}  ethernet2  
${delivery_nick_1}  D1
${delivery_2}  ethernet2  
${delivery_nick_2}  D2
${delivery_3}  ethernet2  
${delivery_nick_3}  D3
${delivery_4}  ethernet2  
${delivery_nick_4}  D4
${service_pre_1}  ethernet47  
${service_prenick_1}  S1PRE
${service_post_1}  ethernet48  
${service_postnick_1}  S1POST
${service_pre_2}  ethernet31 
${service_prenick_2}  S5PRE
${service_post_2}  ethernet32  
${service_postnick_2}  S5POST

*Test Case

Delete match condition in P1 and verify overlap goes away
    rest delete policy match  admin-view  P1  1
    sleep  15
    verify overlap does not exist
    rest add policy match  admin-view  P1  1  {"src-mac": "00:11:00:00:00:01", "ether-type": 2048, "sequence": 1}
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  match 
    
Change action to inactive in P1 and verify overlap goes away
    rest update policy action  admin-view  P1  inactive
    sleep  15
    verify overlap does not exist
    rest update policy action  admin-view  P1  forward
    sleep  15
    verify overlap exists      
    [Tags]  bigtap  overlap  inactive 
    
Set policy action to rate-measure and verify overlap goes away
    rest update policy action  admin-view  P1  rate-measure
    sleep  15    
    verify overlap does not exist
    rest update policy action  admin-view  P1  forward
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  rate-measure 
    
Set different priorities in P1 and P2 and verify overlap goes away
    rest update policy priority  admin-view  P1  200
    sleep  15
    verify overlap does not exist
    rest update policy priority  admin-view  P1  100
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  priority  
    
Add vlan re-write to one policy and verify overlap goes away
    rest add vlan rewrite  admin-view  P1  200
    sleep  15
    verify overlap does not exist
    rest delete vlan rewrite  admin-view  P1  200
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  vlan  
    
Add vlan re-write to both policies and verify overlap stays
    rest add vlan rewrite  admin-view  P1  200
    rest add vlan rewrite  admin-view  ZP1  200    
    sleep  15
    verify overlap exists
    rest delete vlan rewrite  admin-view  P1  200
    rest delete vlan rewrite  admin-view  ZP1  200    
    sleep  15
    verify overlap exists       
    [Tags]  bigtap  overlap  vlan  
    
Create two policies with different filter and delivery interfaces and verify overlap goes away
    rest delete policy interface  admin-view  P1  F1  filter
    rest add policy interface  admin-view  P1  F2  filter
    sleep  15    
    verify overlap does not exist   
    rest delete policy interface  admin-view  P1  F2  filter
    rest add policy interface  admin-view  P1  F1  filter
    sleep  15    
    verify overlap exists
    [Tags]  bigtap  overlap  unique
    
Create two policies with different filter but same delivery interface and verify overlap policy is not created          
    rest delete policy interface  admin-view  P1  F1  filter
    rest delete policy interface  admin-view  ZP1  D2  delivery
    sleep  15
    rest add policy interface  admin-view  P1  F2  filter
    rest add policy interface  admin-view  ZP1  D1  delivery
    sleep  15
    verify overlap does not exist   
    rest delete service from policy  admin-view  P1  SERVICE1
    sleep  15    
    rest delete policy interface  admin-view  P1  F2  filter
    rest delete policy interface  admin-view  ZP1  D1  delivery
    sleep  15
    rest add policy interface  admin-view  P1  F1  filter
    rest add policy interface  admin-view  ZP1  D2  delivery
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  sameDelivery

Add a service to both policies and verify overlap policy is created. Bring down pre-service interface, verify overlap goes away
    rest add service to policy  admin-view  P1  SERVICE5  1
    rest add service to policy  admin-view  ZP1  SERVICE5  1    
    sleep  15
    verify overlap exists
    cli disable interface  s5  ${service_pre_2}
    sleep  15    
    verify overlap does not exist   
    cli enable interface  s5  ${service_pre_2}
    sleep  15
    rest delete service from policy  admin-view  P1  SERVICE5
    rest delete service from policy  admin-view  ZP1  SERVICE5
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  service

Add a service to both policies and verify overlap policy is created. Bring down post-service interface, verify overlap goes away
    rest add service to policy  admin-view  P1  SERVICE5  1
    rest add service to policy  admin-view  ZP1  SERVICE5  1    
    sleep  15
    verify overlap exists
    cli disable interface  s5  ${service_post_2}
    sleep  15    
    verify overlap does not exist  
    cli enable interface  s5  ${service_post_2}
    sleep  15
    rest delete service from policy  admin-view  P1  SERVICE5
    rest delete service from policy  admin-view  ZP1  SERVICE5
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  service

Protocol TCP: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=2048  protocol=6  src_ip=10.28.6.42  dst_ip=10.28.7.44  dst_port=80  src_port=1234  ip_tos=1  vlan=100  
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  tcp  protocolmatches

Protocol TCP6 without range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=34525  protocol=6  src_ip=2001::1  dst_ip=2001::2  dst_port=80  src_port=1234  vlan=100  
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  tcp  v6protocolmatches

Protocol TCP6 with range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=34525  protocol=6  src_ip=2001::1  dst_ip=2001::2  src_port_max=5000  src_port_min=2000  dst_port_max=1024  dst_port_min=1  ip_tos=1  
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  tcp  v6protocolmatches

Protocol UDP: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=2048  protocol=17  src_ip=10.28.6.42  dst_ip=10.28.7.44  dst_port=53  src_port=1234  ip_tos=1  vlan=100
    verify protocol matches  ${generate_protocol_matches}
    [Tags]  bigtap  overlap  udp  protocolmatches

Protocol UDP6 without range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=34525  protocol=17  src_ip=2001::1  dst_ip=2001::2  dst_port=53  src_port=1234  vlan=100  
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  tcp  v6protocolmatches

Protocol UDP6 with range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=34525  protocol=17  src_ip=2001::1  dst_ip=2001::2  src_port_max=5000  src_port_min=2000  dst_port_max=1024  dst_port_min=1  ip_tos=1  
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  tcp  v6protocolmatches
    
Protocol ICMP: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=2048  protocol=1  src_ip=10.28.6.42  dst_ip=10.28.7.44  src_port=8  dst_port=0  ip_tos=1  vlan=100
    verify protocol matches  ${generate_protocol_matches}                
    [Tags]  bigtap  overlap  icmp  protocolmatches

Protocol SCTP: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ip_type=2048  protocol=132  src_ip=10.28.6.42  dst_ip=10.28.7.44  ip_tos=1
    verify protocol matches  ${generate_protocol_matches}                    
    [Tags]  bigtap  overlap  sctp  protocolmatches

L2 without vlan-range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  ether_type=34987  dst_mac=00:11:00:00:00:01  src_mac=00:11:00:00:00:02  vlan=100
    verify protocol matches  ${generate_protocol_matches}                    
    [Tags]  bigtap  overlap  l2  protocolmatches

L2 with vlan-range: All possible matches. Configure two policies with common filter and different delivery interfaces.
    ${generate_protocol_matches}=  return match  dst_mac=00:11:00:00:00:01  src_mac=00:11:00:00:00:02  vlan_min=1  vlan_max=4094 
    verify protocol matches  ${generate_protocol_matches}                    
    [Tags]  bigtap  overlap  l2  protocolmatches

Verify overlap is not created when P1 and ZP1 have different protocols 
    rest delete policy match  admin-view  P1  1
    rest delete policy match  admin-view  ZP1  1
    sleep  15
    rest add policy match  admin-view  P1  1  {"ip-proto": 6, "ether-type": 2048,"sequence": 1,"dst-ip":"192.0.0.1", "dst-ip-mask":"255.255.255.0", "dst-tp-port":80,"src-ip":"192.0.0.2", "src-ip-mask":"255.255.255.0","src-tp-port":1234}
    rest add policy match  admin-view  ZP1  1  {"ip-proto": 17, "ether-type": 2048,"sequence": 1,"dst-ip":"192.0.0.1", "dst-ip-mask":"255.255.255.0","dst-tp-port":80,"src-ip":"192.0.0.2", "src-ip-mask":"255.255.255.0","src-tp-port":1234}                                
    sleep  15
    verify component policies exist    
    verify overlap does not exist
    rest add policy match  admin-view  P1  1  {"src-mac": "00:11:00:00:00:01", "ether-type": 2048, "sequence": 1}
    rest add policy match  admin-view  ZP1  1  {"dst-mac": "00:11:00:00:00:02", "ether-type": 2048, "sequence": 1}    
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  diffmatch

Verify overlap is not created when P1 has IPv4 matches and ZP1 has IPv6 matches
    rest delete policy match  admin-view  P1  1
    rest delete policy match  admin-view  ZP1  1
    sleep  15
    rest add policy match  admin-view  P1  1  {"ip-proto": 6, "ether-type": 2048,"sequence": 1,"dst-ip":"192.0.0.1", "dst-ip-mask":"255.255.255.0","dst-tp-port":80,"src-ip":"192.0.0.2", "src-ip-mask":"255.255.255.0","src-tp-port":1234}
    rest add policy match  admin-view  ZP1  1  {"ip-proto": 6, "ether-type": 34525,"sequence": 1,"dst-ip":"2001::2", "dst-ip-mask":"ffff:ffff:ffff:ffff:0:0:0:0","dst-tp-port":80,"src-ip":"2001::1", "src-ip-mask":"ffff:ffff:ffff:ffff:0:0:0:0","src-tp-port":1234}                                
    sleep  15
    verify component policies exist
    verify overlap does not exist
    rest add policy match  admin-view  P1  1  {"src-mac": "00:11:00:00:00:01", "ether-type": 2048, "sequence": 1}
    rest add policy match  admin-view  ZP1  1  {"dst-mac": "00:11:00:00:00:02", "ether-type": 2048, "sequence": 1}    
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  diffmatch

Configure 4 IPv4 policies and verify overlap is created
    rest update policy action  admin-view  P1  inactive
    rest update policy action  admin-view  ZP1  inactive    
    sleep  5
    add policy  A  ${filter_nick_1}  ${delivery_nick_1}  {"src-ip-mask": "255.255.255.0", "ether-type": 2048, "src-ip": "10.192.93.1", "sequence": 1}
    add policy  B  ${filter_nick_1}  ${delivery_nick_2}  {"sequence": 1, "ether-type": 2048, "dst-ip-mask": "255.255.255.0", "dst-ip": "10.192.93.2"}
    add policy  C  ${filter_nick_1}  ${delivery_nick_3}  {"vlan": 100, "sequence": 1, "ether-type": 2048}
    add policy  D  ${filter_nick_1}  ${delivery_nick_4}  {"ip-tos": 1, "ether-type": 2048, "sequence": 1}
    sleep  5               
    ${verify_overlap1}=  rest verify bigtap policy  _D_o_A_o_B_o_C  num_filter_intf=1  num_delivery_intf=4
    Should be true  ${verify_overlap1}
    ${verify_overlap2}=  rest verify bigtap policy  _A_o_B_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap2}
    ${verify_overlap3}=  rest verify bigtap policy  _D_o_A_o_B  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap3}
    ${verify_overlap4}=  rest verify bigtap policy  _D_o_A_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap4}
    ${verify_overlap5}=  rest verify bigtap policy  _D_o_B_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap5}
    rest delete policy  admin-view  A
    rest delete policy  admin-view  B
    rest delete policy  admin-view  C
    rest delete policy  admin-view  D        
    rest update policy action  admin-view  P1  forward
    rest update policy action  admin-view  ZP1  forward    
    sleep  5                   
    verify overlap exists
    [Tags]  bigtap  overlap  maxmatch

Configure 4 IPv6 policies and verify overlap is created
    rest update policy action  admin-view  P1  inactive
    rest update policy action  admin-view  ZP1  inactive    
    sleep  5
    add policy  A  ${filter_nick_1}  ${delivery_nick_1}  {"src-ip-mask": "ffff:ffff:ffff:ffff:ffff:ffff:ffff:ffff", "ether-type": 34525, "src-ip": "2001::1", "sequence": 1}
    add policy  B  ${filter_nick_1}  ${delivery_nick_2}  {"sequence": 1, "ether-type": 34525, "dst-ip-mask": "ffff:ffff:ffff:ffff:ffff:ffff:ffff:ffff", "dst-ip": "2001::2"}
    add policy  C  ${filter_nick_1}  ${delivery_nick_3}  {"vlan": 100, "sequence": 1, "ether-type": 34525}
    add policy  D  ${filter_nick_1}  ${delivery_nick_4}  {"ip-tos": 1, "ether-type": 34525, "sequence": 1}
    sleep  5               
    ${verify_overlap1}=  rest verify bigtap policy  _D_o_A_o_B_o_C  num_filter_intf=1  num_delivery_intf=4
    Should be true  ${verify_overlap1}
    ${verify_overlap2}=  rest verify bigtap policy  _A_o_B_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap2}
    ${verify_overlap3}=  rest verify bigtap policy  _D_o_A_o_B  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap3}
    ${verify_overlap4}=  rest verify bigtap policy  _D_o_A_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap4}
    ${verify_overlap5}=  rest verify bigtap policy  _D_o_B_o_C  num_filter_intf=1  num_delivery_intf=3
    Should be true  ${verify_overlap5}
    rest delete policy  admin-view  A
    rest delete policy  admin-view  B
    rest delete policy  admin-view  C
    rest delete policy  admin-view  D        
    rest update policy action  admin-view  P1  forward
    rest update policy action  admin-view  ZP1  forward    
    sleep  5                   
    verify overlap exists
    [Tags]  bigtap  overlap  maxmatch
                
Disable overlap knob and verify overlapping policies are deleted
    rest disable feature  overlap
    sleep  15
    verify overlap does not exist
    [Tags]  bigtap  overlap  disableEnable
    
Re-enable previously disabled overlap knob and verify overlapping policies are re-created
    rest enable feature  overlap
    sleep  15
    verify overlap exists
    [Tags]  bigtap  overlap  disableEnable

Shutdown filter interface and verify overlapping policy goes away
    cli disable interface  s1  ${filter_1}
    sleep  15  
    verify overlap exists
    [Tags]  bigtap  overlap  shutUnshut
    
Re-enable previously shut filter interface and verify overlapping policy is re-created
    cli enable interface  s1  ${filter_1}
    sleep  5  
    verify overlap exists
    [Tags]  bigtap  overlap  shutUnshut
    
Shutdown delivery interface and verify overlapping policy goes away
    cli disable interface  s2  ${delivery_2}
    sleep  15  
    verify overlap exists
    [Tags]  bigtap  overlap  shutUnshut
    
Re-enable previously shut delivery interface and verify overlapping policy is re-created
    cli enable interface  s2  ${delivery_2}
    sleep  5  
    verify overlap exists
    [Tags]  bigtap  overlap  shutUnshut

Delete and re-create policies P1 and ZP1 and verify overlap polices are deleted and re-created
    rest delete policy  admin-view  P1
    rest delete policy  admin-view  ZP1
    sleep  5
    verify overlap does not exist
    add policy  P1  ${filter_nick_1}  ${delivery_nick_1}  {"src-mac": "00:11:00:00:00:01", "ether-type": 2048, "sequence": 1}
    add policy  ZP1  ${filter_nick_1}  ${delivery_nick_2}  {"dst-mac": "00:11:00:00:00:02", "ether-type": 2048, "sequence": 1}
    sleep  5  
    verify overlap exists
    [Tags]  bigtap  overlap  delPol 

Execute HA Failover and verify overlapping policies still exist 
    rest execute ha failover
    sleep  150
    verify overlap exists  
    [Tags]  bigtap  overlap  ha

Change configuration execute HA Failback and verify overlapping policies still exist
    rest delete policy match  admin-view  P1  1
    sleep  15
    rest add policy match  admin-view  P1  1  {"src-mac": "00:12:00:00:00:01", "ether-type": 2048, "sequence": 1}
    sleep  15         
    rest execute ha failover
    sleep  150
    verify overlap exists    
    [Tags]  bigtap  overlap  ha

Reboot filter switch and verify overlapping policies still exist
    cli restart switch  s1  save_config=yes
    sleep  50
    verify overlap exists   
    [Tags]  bigtap  overlap  rebootSwitch

Reboot delivery switch and verify overlapping policies still exist
    cli restart switch  s2  save_config=yes
    sleep  50    
    verify overlap exists  
    [Tags]  bigtap  overlap  rebootSwitch 

Reboot core switch and verify overlapping policies still exist
    cli restart switch  s5  save_config=yes    
    verify overlap exists
    [Tags]  bigtap  overlap  rebootSwitch            
                                          
* Keywords

overlap suite setup
    base suite setup
    rest add switch alias  s1  APP-REGRESS-1
    rest add switch alias  s2  APP-REGRESS-2
    rest add switch alias  s3  APP-REGRESS-3
    rest add switch alias  s4  APP-REGRESS-4      
    rest add switch alias  s5  APP-REGRESS-5
    rest add switch alias  s6  APP-REGRESS-6  
    rest add switch alias  s7  APP-REGRESS-7        
    rest add interface role  s1  ${filter_1}  filter  ${filter_nick_1}
    rest add interface role  s2  ${filter_2}  filter  ${filter_nick_2}          
    rest add interface role  s1  ${delivery_1}  delivery  ${delivery_nick_1}
    rest add interface role  s2  ${delivery_2}  delivery  ${delivery_nick_2}
    rest add interface role  s3  ${delivery_3}  delivery  ${delivery_nick_3}
    rest add interface role  s4  ${delivery_4}  delivery  ${delivery_nick_4}    
    rest add interface role  s1  ${service_pre_1}   service  ${service_prenick_1}
    rest add interface role  s1  ${service_post_1}  service  ${service_postnick_1}
    rest add service  SERVICE1  ${service_prenick_1}  ${service_postnick_1} 
    rest add interface role  s5  ${service_pre_2}   service  ${service_prenick_2}
    rest add interface role  s5  ${service_post_2}  service  ${service_postnick_2}
    rest add service  SERVICE5  ${service_prenick_2}  ${service_postnick_2}          
    Sleep  5
    rest add policy  admin-view  P1  forward
    rest add policy match  admin-view  P1  1  {"src-mac": "00:11:00:00:00:01", "ether-type": 2048, "sequence": 1}
    rest add policy interface  admin-view  P1  F1  filter
    rest add policy interface  admin-view  P1  D1  delivery 
    rest add policy  admin-view  ZP1  forward
    rest add policy match  admin-view  ZP1  1  {"dst-mac": "00:11:00:00:00:02", "ether-type": 2048, "sequence": 1}
    rest add policy interface  admin-view  ZP1  F1  filter
    rest add policy interface  admin-view  ZP1  D2  delivery
    Sleep  5

add policy  [Arguments]  ${policy_name}  ${filter}  ${delivery}  ${match_condition}  
    rest add policy  admin-view  ${policy_name}  forward
    rest add policy match  admin-view  ${policy_name}  1  ${match_condition}
    rest add policy interface  admin-view  ${policy_name}  ${filter}  filter
    rest add policy interface  admin-view  ${policy_name}  ${delivery}  delivery 

verify overlap exists
    ${verify_policy1}=  rest verify bigtap policy  P1  num_filter_intf=1  num_delivery_intf=1
    Should be true  ${verify_policy1}
    ${verify_policy2}=  rest verify bigtap policy  ZP1  num_filter_intf=1  num_delivery_intf=1
    Should be true  ${verify_policy2}
    ${verify_policy12}=  rest verify bigtap policy  _ZP1_o_P1  num_filter_intf=1  num_delivery_intf=2
    Should be true  ${verify_policy12}

verify component policies exist
    ${verify_policy1}=  rest verify bigtap policy  P1  num_filter_intf=1  num_delivery_intf=1
    Should be true  ${verify_policy1}
    ${verify_policy2}=  rest verify bigtap policy  ZP1  num_filter_intf=1  num_delivery_intf=1
    Should be true  ${verify_policy2}
    
verify overlap does not exist
    ${verify_overlap}=  rest verify bigtap policy  _ZP1_o_P1  num_filter_intf=1  num_delivery_intf=2
    Should not be true  ${verify_overlap}
        
verify protocol matches  [Arguments]  ${match_dictionary}
    ${length}=  Get Length  ${match_dictionary}
    : FOR    ${INDEX}    IN RANGE    0    ${length}/2
    \    ${NINDEX}=  Evaluate  ${INDEX} + ${length}/2  
    \    Log    ${match_dictionary[${INDEX}]}
    \    Log    ${match_dictionary[${NINDEX}]}
    \    rest delete policy match  admin-view  P1  1
    \    rest delete policy match  admin-view  ZP1  1
    \    sleep  15
    \    rest add policy match  admin-view  P1  1  ${match_dictionary[${INDEX}]}  flag=True
    \    rest add policy match  admin-view  ZP1  1  ${match_dictionary[${NINDEX}]}  flag=True
    \    sleep  15
    \    ${verify_overlap}=  rest verify bigtap policy  _ZP1_o_P1  num_filter_intf=1  num_delivery_intf=2
    \    Should be true  ${verify_overlap} 

overlap suite teardown
    rest delete policy  admin-view  P1
    rest delete policy  admin-view  ZP1
    rest delete service  SERVICE1
    rest delete service  SERVICE5
    rest delete interface role  s1  ${service_pre_1}   service
    rest delete interface role  s1  ${service_post_1}   service
    rest delete interface role  s5  ${service_pre_2}   service
    rest delete interface role  s5  ${service_post_2}   service              
    rest delete interface role  s1  ${filter_1}    filter          
    rest delete interface role  s2  ${filter_2}    filter        
    rest delete interface role  s1  ${delivery_1}    delivery
    rest delete interface role  s2  ${delivery_2}    delivery
    rest delete interface  s1  ${filter_1}          
    rest delete interface  s2  ${filter_2}        
    rest delete interface  s1  ${delivery_1}          
    rest delete interface  s2  ${delivery_2}
    rest delete switch alias  s1
    rest delete switch alias  s2
    rest delete switch alias  s3
    rest delete switch alias  s4
    rest delete switch alias  s5
    rest delete switch alias  s6
    rest delete switch alias  s7    
    rest delete switch  s1
    rest delete switch  s2
    rest delete switch  s3
    rest delete switch  s4
    rest delete switch  s5
    rest delete switch  s6
    rest delete switch  s7                                       
    base suite teardown    