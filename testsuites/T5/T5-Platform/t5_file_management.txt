*** Settings ***
Documentation  		Testing File Management - copying RC to other locations
Suite Setup  		File Suite Setup
Suite Teardown   	File Suite Teardown
Test Setup   		Setup
Test Teardown  		Teardown
Force Tags   		Full  Feature  FileManagement  Platform  T5  IronHorse
Library  			keywords/BsnCommon.py
Library  			keywords/T5Platform.py
Library  			keywords/T5Utilities.py
Library  			keywords/T5.py
Library        		String


*** Variables ***
${release}                          JF300
${scp server}                       scp://bsn@10.9.16.52
${http server}                      http://10.2.3.11
${temp}                             ${scp server}:/tmp/scp
${scp}                              ${scp server}:file-management-testsuite/${release}/scp
${scp RC}                           ${scp server}:file-management-testsuite/${release}/config-RC
${scp file}                         ${scp server}:file-management-testsuite/${release}/config-file
${scp config}                       ${scp server}:file-management-testsuite/${release}/config-config-scp
${scp double}                       scp://${scp server}:file-management-testsuite/${release}/config-scp
${scp location}                     bsn@10.9.16.52:file-management-testsuite/${release}/config-scp
${scp inexistent}                   ${scp server}:file-management-testsuite/${release}/inexistent
${scp switch}                       ${scp server}:file-management-testsuite/${release}/scp-config-large
${scp extensive file}               ${scp server}:file-management-testsuite/${release}/extensive-file
${scp extensive config}             ${scp server}:file-management-testsuite/${release}/extensive-config
${scp extensive config start}       ${scp server}:file-management-testsuite/${release}/extensive-config-start
${http extensive file}              ${http server}/~bsn/file-management-testsuite/${release}/extensive-file
${http extensive config}            ${http server}/~bsn/file-management-testsuite/${release}/extensive-config
${http extensive config start}      ${http server}/~bsn/file-management-testsuite/${release}/extensive-config-start
${http inexistent}                  ${http server}/~bsn/file-management-testsuite/${release}/inexistent

${file}                             file://config-file
${config}                           snapshot://config-config
${config double}                    snapshot://snapshot://config-config
${config name}                      config-config
${image}                            image://image-file
${image double}                     image://image://image-file
${scp image}                        ${scp server}:file-management-testsuite/${release}/controller-upgrade-bvs-2.0.8-SNAPSHOT.pkg
${scp image corrupted}              ${scp server}:file-management-testsuite/${release}/controller-upgrade-bvs-2.0.8-SNAPSHOT-corrupted.pkg
${scp image bigtap}                 ${scp server}:file-management-testsuite/${release}/bigtap-3.1.0-upgrade-2014.04.28.2234.pkg
@{spineList}                        00:00:00:00:00:01:00:01  00:00:00:00:00:01:00:02
@{leafList}                         00:00:00:00:00:02:00:01  00:00:00:00:00:02:00:02  00:00:00:00:00:02:00:03  00:00:00:00:00:02:00:04


*** Test Cases ***
T0.1 Extensive config and file compare locally
	Cli Copy  ${scp extensive config}  snapshot://extensive-config
	Cli Copy  ${scp extensive file}  file://extensive-file
	Compare  file://extensive-file  snapshot://extensive-config

T0.2 Extensive config remote copy
	Cli Copy  ${scp extensive config}  running-config
	Compare  ${scp extensive config}  running-config
	Cli Copy  ${scp extensive config}  snapshot://extensive-config
	Compare  snapshot://extensive-config  running-config
	[Tags]  BSC-5370

T0.3 Extensive file remote copy
	Rest Delete Tenant  TenantTest12345
	Cli Copy  ${scp extensive file}  running-config
	Sleep  10
	Compare  ${scp extensive file}  running-config
	Compare  snapshot://extensive-config  running-config
	Sleep  10
	Cli Copy  ${scp extensive file}  file://extensive-file
	Compare  file://extensive-file  running-config

T0.4 Extensive config remote replace
	Cli Copy  ${scp extensive config start}  running-config
	Sleep  10
	Compare  ${scp extensive config start}  running-config
	Sleep  10
	Cli Copy  ${scp extensive config start}  snapshot://extensive-config-start
	Compare  snapshot://extensive-config-start  running-config

	Cli Copy  ${scp extensive config}  running-config
	Sleep  10
	Compare  ${scp extensive config}  running-config
	Sleep  10
	Cli Copy  ${scp extensive config}  snapshot://extensive-config
	Compare  snapshot://extensive-config  running-config
	[Tags]  BSC-5370  BSC-5488

T0.5 Extensive config local copy
	Cli Copy  ${scp extensive config}  snapshot://extensive-config
	Cli Copy  snapshot://extensive-config  running-config
	Sleep  10
	Compare  ${scp extensive config}  running-config
	Compare  snapshot://extensive-config  running-config
	[Tags]  BSC-5488

T0.6 Extensive file local copy
	Rest Delete Tenant  TenantTest12345
	Cli Copy  ${scp extensive file}  file://extensive-file
	Cli Copy  file://extensive-file  running-config
	Sleep  10
	Compare  ${scp extensive file}  running-config
	Compare  file://extensive-file  running-config

T0.7 Extensive config local replace
	Cli Copy  ${scp extensive config start}  snapshot://extensive-config-start
	Cli Copy  snapshot://extensive-config-start  running-config
	Cli Run  master  show running-config
	Sleep  10
	Compare  ${scp extensive config start}  running-config
	Compare  snapshot://extensive-config-start  running-config

	Sleep  10
	Cli Copy  ${scp extensive config}  snapshot://extensive-config
	Cli Copy  snapshot://extensive-config  running-config
	Cli Run  master  show running-config
	Sleep  10
	Compare  ${scp extensive config}  running-config
	Compare  snapshot://extensive-config  running-config
	[Tags]  BSC-5488

T0.8 Extensive config and file compare locally from http
    Cli Copy  ${http extensive config}  snapshot://extensive-config-http
    Cli Copy  ${http extensive file}  file://extensive-file-http
    Compare  file://extensive-file-http  snapshot://extensive-config-http
    [Tags]  HTTP

T0.9 Extensive config remote copy from http
    Cli Copy  ${http extensive config}  running-config
    Compare  ${http extensive config}  running-config
    Cli Copy  ${http extensive config}  snapshot://extensive-config
    Compare  snapshot://extensive-config  running-config
    [Tags]  HTTP

T0.10 Extensive file remote copy
    Rest Delete Tenant  TenantTest12345
    Cli Copy  ${http extensive file}  running-config
    Sleep  10
    Compare  ${http extensive file}  running-config
    Compare  snapshot://extensive-config  running-config
    Sleep  10
    Cli Copy  ${http extensive file}  file://extensive-file
    Compare  file://extensive-file  running-config
    [Tags]  HTTP

T0.11 Extensive config remote replace
    Cli Copy  ${http extensive config start}  running-config
    Sleep  10
    Compare  ${http extensive config start}  running-config
    Sleep  10
    Cli Copy  ${http extensive config start}  snapshot://extensive-config-start
    Compare  snapshot://extensive-config-start  running-config

    Cli Copy  ${http extensive config}  running-config
    Sleep  10
    Compare  ${http extensive config}  running-config
    Sleep  10
    Cli Copy  ${http extensive config}  snapshot://extensive-config
    Compare  snapshot://extensive-config  running-config
    [Tags]  HTTP

T0.12 Extensive config local copy
    Cli Copy  ${http extensive config}  snapshot://extensive-config
    Cli Copy  snapshot://extensive-config  running-config
    Sleep  10
    Compare  ${http extensive config}  running-config
    Compare  snapshot://extensive-config  running-config
    [Tags]  HTTP

T0.13 Extensive file local copy
    Rest Delete Tenant  TenantTest12345
    Cli Copy  ${http extensive file}  file://extensive-file
    Cli Copy  file://extensive-file  running-config
    Sleep  10
    Compare  ${http extensive file}  running-config
    Compare  file://extensive-file  running-config
    [Tags]  HTTP

T0.14 Extensive config local replace
    Cli Copy  ${http extensive config start}  snapshot://extensive-config-start
    Cli Copy  snapshot://extensive-config-start  running-config
    Cli Run  master  show running-config
    Sleep  10
    Compare  ${http extensive config start}  running-config
    Compare  snapshot://extensive-config-start  running-config

    Sleep  10
    Cli Copy  ${http extensive config}  snapshot://extensive-config
    Cli Copy  snapshot://extensive-config  running-config
    Cli Run  master  show running-config
    Sleep  10
    Compare  ${http extensive config}  running-config
    Compare  snapshot://extensive-config  running-config
    [Tags]  HTTP

T1.1 Copy running config to file
	Cli Copy  running-config  ${file}
	Compare RC with File Line By Line  ${file}
	Compare  running-config  ${file}
	[Tags]  Smoke

T1.2 Copy running config to snapshot://
	Cli Copy  running-config  ${config}
	Compare RC with Snapshot Line By Line  ${config}
	Compare  running-config  ${config}
	[Tags]  Smoke

T1.3 Copy running config to scp://
	${scp_random}  Generate Random Name  ${temp}  5
	Cli Copy  running-config  ${scp_random}
	Compare  running-config  ${scp_random}
	Compare Slave  running-config  ${scp_random}
	[Tags]  SCP  Smoke  HA


T2.1 Copy running config to file, change RC, verify configs different
	Cli Copy  running-config  ${file}
	Modify Running Config
	Compare RC with File Line By Line Negative  ${file}
	Compare Negative  running-config  ${file}
	[Tags]

T2.2 Copy running config to snapshot://, change RC, verify configs different
	Cli Copy  running-config  ${config}
	Modify Running Config
	Compare RC with Snapshot Line By Line Negative  ${config}
	Compare Negative  running-config  ${config}
	[Tags]

T2.3 Copy running config to scp://, change RC, verify configs different
	Cli Copy  running-config  ${scp}
	Modify Running Config
	Compare Negative  running-config  ${scp}
	Compare Negative Slave  running-config  ${scp}
	[Tags]  SCP  HA


T3.1 Verify deletion of file
	Cli Copy  running-config  ${file}
	Cli Delete  ${file}
	[Tags]  Smoke

T3.2 Verify deletion of inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: File "*" not found*  Cli Delete  inexistent

T3.3 Verify deletion of snapshot://file
	Cli Copy  running-config  ${config}
	Cli Delete  ${config}
	[Tags]  Smoke

T3.4 Verify deletion of inexistent snapshot://file
	Run Keyword and Expect Error  *Error: Invalid Use: * does not exist*  Cli Delete  snapshot://inexistent


T4.1.1 Copy running config to file, add elements to RC, restore, verify (new elements are kept)
	Rest Add Tenant  TempTenant123
	Cli Copy  running-config  ${file}
	${tenant}  Generate Random Name  Tenant  5
	Rest Add Tenant  ${tenant}
	Cli Copy  running-config  file://backup
	Rest Delete Tenant  TempTenant123
	Cli Copy  ${file}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  file://backup
	Should Be True  ${status}
	Compare  running-config  file://backup
	[Tags]  Smoke
T4.1.2 Copy running config to file, delete elements from RC, restore, verify (deleted elements are restored)
	Cli Copy  running-config  ${file}
	Cli Copy  snapshot://startup-config  running-config
	Cli Copy  running-config  file://backup
	Cli Copy  ${file}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  file://backup
	Should Not Be True  ${status}
	Compare Negative  running-config  file://backup
	[Tags]  Smoke

T4.2 Copy running config to snapshot://, change RC, restore, verify old RC restored
	Cli Copy  running-config  ${config}
	Modify Running Config
	Cli Copy  ${config}  running-config
	Compare RC with Snapshot Line By Line  ${config}
	${status} =  Cli Compare Running Config with Snapshot Line By Line  ${config name}
	Should Be True  ${status}
	Compare  running-config  ${config}
	[Tags]

T4.3 Copy running config to scp://, change RC, restore, verify no change in RC
	Cli Copy  running-config  ${scp}
	Modify Running Config
	Cli Copy  running-config  file://backup
	Cli Copy  ${scp}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  file://backup
	Should Not Be True  ${status}
	Compare Negative  running-config  ${scp}
	Compare Negative  running-config  file://backup
	[Tags]  SCP  Smoke

T4.4 Copy from scp:// and compare with previous running-config
	Rest Delete Tenant  TenantTest12345
	Cli Copy  snapshot://startup-config  running-config
	Cli Copy  running-config  file://backup
	Cli Copy  ${scp switch}  running-config
	Compare Negative  file://backup  running-config
	Sleep  5
	Compare  ${scp switch}  running-config
	[Tags]  SCP


T5.1 Copy running config to file, copy RC from file to other file, verify
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  file://file-new
	${status} =  Cli Compare Running Config with File Line By Line  file://file-new
	Should Be True  ${status}
	Compare  running-config  file://file-new

T5.2 Copy running config to file, copy RC from file to scp://, verify
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp}
	Sleep  5
	Compare  running-config  ${scp}
	Sleep  5
	Compare Slave  running-config  ${scp}
	[Tags]  SCP  HA

T5.3 Copy running config to snapshot://, copy RC from snapshot:// to file, verify
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: file not config file*  Cli Copy  ${file}  snapshot://config-new

T5.4 Copy running config to snapshot://, copy RC from snapshot:// to scp://, verify
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp}
	Sleep  5
	Compare  ${config}  ${scp}
	Sleep  5
	Compare  running-config  ${scp}
	Sleep  5
	Compare Slave  running-config  ${scp}
	[Tags]  BSC-4955  SCP  HA

T5.5 Copy running config to scp://, copy RC from scp:// to file, verify
	Cli Copy  running-config  ${scp}
	Sleep  5
	Cli Copy  ${scp}  ${file}
	Sleep  5
	Compare  running-config  ${file}
	[Tags]  SCP

T5.6.1 Copy running config to scp://, copy RC from scp:// to image, verify
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: Image target must only be image://*  Cli Copy  ${scp}  ${image}
	[Tags]  BSC-4956  SCP
T5.6.2 Copy running config to scp://, copy RC from scp:// to image, verify
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: integrity validation failed: not an image bundle*  Cli Copy  ${scp}  image://
	[Tags]  BSC-4956  SCP

T5.7 Copy running config to snapshot://, copy snapshot:// to scp://, copy from scp:// to file, copy file to snapshot://, copy snapshot:// to RC
	Cli Copy  running-config  ${config}
	Modify Running Config
	Cli Copy  running-config  file://backup
	Cli Copy  ${config}  ${scp}
	Cli Copy  ${scp}  ${file}
	Cli Copy  ${file}  snapshot://config-new
	Cli Copy  snapshot://config-new  running-config
	Compare  ${config}  running-config
	Compare Negative  file://backup  running-config
	[Tags]  SCP

T5.8.1 Verify: compare running-config scp://<running-config>
	Cli Copy  running-config  ${scp RC}
	Compare  running-config  ${scp RC}
	Compare Slave  running-config  ${scp RC}
	[Tags]  SCP  HA

T5.8.2 Verify: compare running-config scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Compare  running-config  ${scp file}
	Compare Slave  running-config  ${scp file}
	[Tags]  SCP  HA

T5.8.3 Verify: compare running-config scp://<snapshot://>
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  running-config  ${scp config}
	Compare Slave  running-config  ${scp config}
	[Tags]  SCP  HA

T5.8.4 Verify: compare <file> scp://<running-config>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${scp RC}
	Compare  ${file}  ${scp RC}
	[Tags]  SCP

T5.8.5 Verify: compare <file> scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Compare  ${file}  ${scp file}
	[Tags]  SCP

T5.8.6 Verify: compare <file> scp://<snapshot://>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  ${file}  ${scp config}
	[Tags]  SCP

T5.8.7 Verify: compare <snapshot://> scp://<running-config>
	Cli Copy  running-config  ${config}
	Cli Copy  running-config  ${scp RC}
	Compare  ${config}  ${scp RC}
	[Tags]  SCP

T5.8.8 Verify: compare <snapshot://> scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Cli Copy  ${file}  ${scp file}
	Compare  ${config}  ${scp file}
	[Tags]  SCP

T5.8.9 Verify: compare <snapshot://> scp://<snapshot://>
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  ${config}  ${scp config}
	[Tags]  SCP

T5.8.10 Verify: compare scp:// image://
	Cli Copy  running-config  ${scp file}
	Run Keyword and Expect Error
	...  *Error: Invalid Use: unsupported compare scp://* for scp*
	...  Cli Compare  master  ${scp file}  image://
	[Tags]  SCP  Image

T5.8.11 Verify: compare <http://> scp://<running-config>
    Run Keyword and Expect Error
    ...  *Error: Invalid Use: scp://* not supported*
    ...  Compare  ${http extensive config}  ${scp RC}
    [Tags]  SCP  HTTP

T5.9.1 Verify: copy scp://<running-config> running-config
	Cli Copy  running-config  ${scp RC}
	Cli Copy  ${scp RC}  running-config
	Compare  ${scp RC}  running-config
	[Tags]  SCP

T5.9.2 Verify: copy scp://<running-config> file
	Cli Copy  running-config  ${scp RC}
	Cli Copy  ${scp RC}  ${file}
	Compare  ${scp RC}  ${file}
	[Tags]  SCP

T5.9.3 Verify: copy scp://<running-config> snapshot://
	Cli Copy  running-config  ${scp RC}
	Run Keyword and Expect Error  *Error: copy: scp://* invalid snapshot file*  Cli Copy  ${scp RC}  ${config}
	[Tags]  SCP

T5.9.4 Verify: copy scp://<file> running-config
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Cli Copy  ${scp file}  running-config
	Compare  ${scp file}  running-config
	[Tags]  SCP

T5.9.5 Verify: copy scp://<file> file
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Cli Copy  ${scp file}  ${file}
	Compare  ${scp file}  ${file}
	[Tags]  SCP

T5.9.6 Verify: copy scp://<file> snapshot://
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Run Keyword and Expect Error  *Error: copy: scp://* invalid snapshot file*  Cli Copy  ${scp file}  ${config}
	[Tags]  SCP

T5.9.7 Verify: copy scp://<snapshot://> running-config
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  running-config
	Compare  ${scp config}  running-config
	[Tags]  SCP  BSC-5283

T5.9.8 Verify: copy scp://<snapshot://> file - negative, this is not supported
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  ${file}
	Compare Negative  ${scp config}  ${file}
	[Tags]  SCP

T5.9.9 Verify: copy scp://<snapshot://> config
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  ${config}
	Compare  ${scp config}  ${config}
	[Tags]  SCP


T5.10.1 Verify: copy & compare http://<file://> running-config
    Rest Delete Tenant  TenantTest12345
    Cli Copy  ${http extensive file}  running-config
    Compare  ${http extensive file}  running-config
    Modify Running Config
    Compare Negative  ${http extensive file}  running-config
    [Tags]  HTTP

T5.10.2 Verify: copy & compare http://<file://> file
    Cli Copy  ${http extensive file}  ${file}
    Compare  ${http extensive file}  ${file}
    Modify Running Config
    Cli Copy  running-config  ${file}
    Compare Negative  ${http extensive file}  ${file}
    [Tags]  HTTP  BSC-7172

T5.10.3 Verify: copy & compare http://<file://> config
    Run Keyword and Expect Error
    ...  *invalid snapshot file*
    ...  Cli Copy  ${http extensive file}  ${config}
    Cli Copy  ${http extensive file}  running-config
    Modify Running Config
    Cli Copy  running-config  ${config}
    Compare Negative  ${http extensive file}  ${config}
    [Tags]  HTTP

T5.10.4 Verify: copy & compare http://<snapshot://> running-config
    Cli Copy  ${http extensive config}  running-config
    Compare  ${http extensive config}  running-config
    Modify Running Config
    Compare Negative  ${http extensive config}  running-config
    [Tags]  HTTP

T5.10.5 Verify: copy & compare http://<snapshot://> file
    Cli Copy  ${http extensive config}  ${file}
    Compare  ${http extensive config}  ${file}
    Cli Copy  ${http extensive config}  running-config
    Modify Running Config
    Cli Copy  running-config  ${file}
    Compare Negative  ${http extensive config}  ${file}
    [Tags]  HTTP  skipped
    # not a valid test case

T5.10.6 Verify: copy & compare http://<snapshot://> config
    Cli Copy  ${http extensive config}  ${config}
    Compare  ${http extensive config}  ${config}
    Cli Copy  ${http extensive config}  running-config
    Modify Running Config
    Cli Copy  running-config  ${config}
    Compare Negative  ${http extensive config}  ${config}
    [Tags]  HTTP


T6.1.1 Verify Filename containing ~
	Validate Filename  file://test~test
	[Tags]  Filename
T6.1.2 Verify Filename containing !
	Validate Filename  file://test!test
	[Tags]  Filename
T6.1.3 Verify Filename containing -
	Validate Filename  file://test-test
	[Tags]  Filename
T6.1.4 Verify Filename containing _
	Validate Filename  file://test_test
	[Tags]  Filename

T6.2.1 Verify snapshot:// name containing ~
	Validate Filename  snapshot://test~test
	[Tags]  Filename
T6.2.2 Verify snapshot:// name containing !
	Validate Filename  snapshot://test!test
	[Tags]  Filename
T6.2.3 Verify snapshot:// name containing -
	Validate Filename  snapshot://test-test
	[Tags]  Filename
T6.2.4 Verify snapshot:// name containing _
	Validate Filename  snapshot://test_test
	[Tags]  Filename

T6.3 Verify correct processing of special characters in scp:// name (?)
	Cli Copy  running-config  ${scp server}:file-management-testsuite/${release}/test#test
	Compare  running-config  ${scp server}:file-management-testsuite/${release}/test#test
	Cli Copy  ${scp server}:file-management-testsuite/${release}/test#test   running-config
	[Tags]  Filename  SCP

T6.4 Verify empty file name is not allowed
	Run Keyword and Expect Error  *Error: Unexpected end of command*  Cli Copy  running-config  ${EMPTY}
	[Tags]  Filename

T6.5 Verify empty snapshot:// name is not allowed
	Run Keyword and Expect Error  *Error: Invalid Use: copy: snapshot:// requires a filename after slashes*  Cli Copy  running-config  snapshot://
	[Tags]  Filename

T6.6 Verify empty scp:// name is not allowed
	Run Keyword and Expect Error  *Error: Invalid Use: No scp path after scp://*  Cli Copy  running-config  scp://
	[Tags]  Filename  SCP

T6.7 Verify scp://scp:// not correct in copy function
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  running-config  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  running-config
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${file}  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  ${file}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${config}  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  ${config}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  image://
	[Tags]  BSC-4904  SCP  Filename

T6.8 Verify scp://scp:// not correct in compare function
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Compare  master  running-config  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Compare  master  ${scp double}  running-config
	[Tags]  SCP  Filename

T6.9 Verify snapshot://snapshot:// not correct in copy function
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://snapshot://* must not include / *  Cli Copy  running-config  ${config double}
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://snapshot://* must not include / *  Cli Copy  ${config double}  running-config
	[Tags]  Filename

T6.10 Verify snapshot://snapshot:// not correct in compare function
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://snapshot://* must not include / *  Cli Compare  master  running-config  ${config double}
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://snapshot://* must not include / *  Cli Compare  master  ${config double}  running-config
	[Tags]  Filename

T6.11 Verify image-double not correct in copy and compare
	Run Keyword and Expect Error  *Error: Invalid Use: Image target must only be image://*  Cli Copy  ${scp}  image://image://
	[Tags]  BSC-4957  Filename

T6.12 Verify empty http:// name is not allowed
    Run Keyword and Expect Error  *Error: Invalid Use: No path after http://*  Cli Copy  http://  running-config
    Run Keyword and Expect Error  *Error: Invalid Use: No path after http://*  Compare  http://  running-config
    [Tags]  Filename  HTTP

### and check verifications for empty file names + error messages
T7.1 Verify comparison function between running-config and file
	Cli Copy  running-config  ${file}
	Sleep  1
	Compare  running-config  ${file}
	Modify Running Config
	Compare Negative  running-config  ${file}

T7.2 Verify comparison function between running-config and snapshot://
	Cli Copy  running-config  ${config}
	Compare  running-config  ${config}
	Modify Running Config
	Compare Negative  running-config  ${config}

T7.3 Verify comparison function between running-config and scp://
	Cli Copy  running-config  ${scp}
	Compare  running-config  ${scp}
	Compare Slave  running-config  ${scp}
	Modify Running Config
	Compare Negative  running-config  ${scp}
	Compare Negative Slave  running-config  ${scp}
	[Tags]  SCP  HA

T7.4 Verify comparison function between file and snapshot://
	Cli Copy  running-config  ${file}
	Sleep  1
	Cli Copy  running-config  ${config}
	Compare  ${file}  ${config}
	Modify Running Config
	Cli Copy  running-config  file://file-new
	Cli Copy  running-config  snapshot://config-new
	Compare Negative  file://file-new  ${config}
	Compare Negative  ${file}  snapshot://config-new

T7.5 Verify comparison function between file and running-config
	Cli Copy  running-config  ${file}
	Sleep  1
	Compare  ${file}  running-config
	Modify Running Config
	Compare Negative  ${file}  running-config

T7.6 Verify comparison function between file and scp://
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${scp}
	Compare  ${file}  ${scp}
	Modify Running Config
	Cli Copy  running-config  file://file-new
	Cli Copy  running-config  ${scp server}:file-management-testsuite/${release}/config-scp-new
	Compare Negative  file://file-new  ${scp}
	Compare Negative  ${file}  ${scp server}:file-management-testsuite/${release}/config-scp-new
	[Tags]  SCP

T7.7 Verify comparison function between file and file
	Cli Copy  running-config  file://file1
	Sleep  1
	Cli Copy  running-config  file://file2
	Compare  file://file1  file://file2
	Modify Running Config
	Cli Copy  running-config  ${file}
	Compare Negative  file://file1  ${file}
	Compare Negative  file://file2  ${file}

T7.8 Verify comparison function between scp:// and snapshot://
	Cli Copy  running-config  ${config}
	Cli Copy  running-config  ${scp}
	Compare  ${config}  ${scp}
	Modify Running Config
	Cli Copy  running-config  snapshot://config-new
	Cli Copy  running-config  ${scp server}:file-management-testsuite/${release}/config-scp-new
	Compare  snapshot://config-new  ${scp server}:file-management-testsuite/${release}/config-scp-new
	Compare Negative  snapshot://config-new  ${scp}
	Compare Negative  snapshot://config-new  ${config}
	[Tags]  SCP

T7.9 Verify comparison function between snapshot:// and snapshot://
	Cli Copy  running-config  snapshot://config-new1
	Cli Copy  running-config  snapshot://config-new2
	Compare  snapshot://config-new1  snapshot://config-new2
	Modify Running Config
	Cli Copy  running-config  ${config}
	Compare Negative  snapshot://config-new1  ${config}
	Compare Negative  snapshot://config-new2  ${config}

T7.10 Verify comparison function between running-config and inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  running-config  file://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  file://inexistent  running-config

T7.11 Verify comparison function between running-config and inexistent snapshot://file
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  running-config  snapshot://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  snapshot://inexistent  running-config

T7.12 Verify comparison function between running-config and inexistent scp://file
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  running-config  ${scp inexistent}
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  ${scp inexistent}  running-config
	[Tags]  SCP

T7.13 Verify comparison function between file and inexistent snapshot://file
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  ${file}  snapshot://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  snapshot://inexistent  ${file}

T7.14 Verify comparison function between file and inexistent scp://
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  ${file}  ${scp inexistent}
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  ${scp inexistent}  ${file}
	[Tags]  SCP

T7.15 Verify comparison function between file and inexistent file
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  ${file}  file://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  file://inexistent  ${file}

T7.16 Verify comparison function between inexistent file and inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent1 does not exist*
	...  Cli Compare  master  file://inexistent1  file://inexistent2

T7.17 Verify comparison function between snapshot://file and inexistent file
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  ${config}  file://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  file://inexistent  ${config}

T7.18 Verify comparison function between snapshot://file and inexistent scp://
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  ${config}  ${scp inexistent}
	Run Keyword and Expect Error  *scp: file-management-testsuite/${release}/inexistent: No such file or directory*
	...  Cli Compare  master  ${scp inexistent}  ${config}
	[Tags]  SCP

T7.19 Verify comparison function between snapshot://file and inexistent snapshot://
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  ${config}  snapshot://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  snapshot://inexistent  ${config}

T7.20 Verify comparison function between inexistent snapshot://file and inexistent snapshot://
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent1 does not exist*
	...  Cli Compare  master  snapshot://inexistent1  snapshot://inexistent2

T7.21 Verify comparison function between scp:// and inexistent file
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  ${scp}  file://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: file://inexistent does not exist*
	...  Cli Compare  master  file://inexistent  ${scp}
	[Tags]  SCP

T7.22 Verify comparison function between scp:// and inexistent snapshot://file
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  ${scp}  snapshot://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: snapshot://inexistent does not exist*
	...  Cli Compare  master  snapshot://inexistent  ${scp}
	[Tags]  SCP

T7.23 Verify comparison function between scp:// and image:// - negative
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: unsupported compare scp://* for scp*
	...  Cli Compare  master  ${scp}  image://
	[Tags]  SCP



T8.1 Verify deletion of file
	Cli Copy  running-config  ${file}
	Cli Delete  ${file}

T8.2 Verify deletion of inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: File "*" not found*  Cli Delete  inexistent

T8.3 Verify deletion of snapshot://file
	Cli Copy  running-config  ${config}
	Cli Delete  ${config}

T8.4 Verify deletion of inexistent snapshot://file
	Run Keyword and Expect Error  *Error: Invalid Use: * does not exist*  Cli Delete  snapshot://inexistent


T9.1 Verify impossible to copy to invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  running-config  scp://invalid
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  running-config  scp://scp-no-colon@1.2.3.4
	[Tags]  SCP

T9.2 Verify impossible to copy from invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  scp://invalid  running-config
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  scp://scp-no-colon@1.2.3.4  running-config
	[Tags]  SCP

T9.3 Verify impossible to compare with invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Compare  running-config  scp://invalid
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Compare  running-config  scp://scp-no-colon@1.2.3.4
	[Tags]  SCP

T9.4 Verify impossible to copy from inexistent SCP
	Run Keyword and Expect Error  *Error: Invalid Use: file-management-testsuite/${release}/inexistent: No such file or directory*  Cli Copy  ${scp inexistent}  running-config
	[Tags]  SCP

T9.5 Verify impossible to copy from invalid HTTP
    Run Keyword and Expect Error  *urlopen error [Errno -2] Name or service not known*
    ...  Cli Copy  http://aaa  running-config
    [Tags]  HTTP

T9.6 Verify impossible to compare with invalid HTTP
    Run Keyword and Expect Error  *http failed: URL Error: [Errno -2] Name or service not known*
    ...  Compare  running-config  http://aaa
    Run Keyword and Expect Error  *http failed: URL Error: [Errno -2] Name or service not known*
    ...  Compare  http://aaa  running-config
    [Tags]  HTTP

T9.7 Verify impossible to copy from inexistent HTTP
    Run Keyword and Expect Error  *Error: Invalid Use: HTTP Error 404: Not Found*
    ...  Cli Copy  ${http inexistent}  running-config
    [Tags]  HTTP


T10.1 Verify validation function of scp:// to image:// - textfile
	Run Keyword and Expect Error
	...  *Validating Image Contents: check for expected contents*
	...  Cli Copy  ${scp}  image://
	[Tags]  SCP  image  skipped

T10.2 Verify validation function of scp:// to image:// - bigtap upgrade package
	Run Keyword and Expect Error
	...  *Error: Invalid Use: integrity validation failed: missing file in bundle: supported-matrix*
	...  Cli Copy  ${scp image bigtap}  image://
	[Tags]  SCP  image  skipped

T10.3 Verify validation function of scp:// to image:// - corrupted image
	Run Keyword and Expect Error
	...  *Error: Invalid Use: integrity validation failed: not an image bundle*
	...  Cli Copy  ${scp image corrupted}  image://
	[Tags]  SCP  image  skipped

T10.4 Verify validation function of scp:// to image:// - proper image
	Cli Copy  ${scp image}  image://
	Cli Delete  image://master
	[Tags]  SCP  image  skipped

T10.5 Verify suggestions for deleting multiple images
	Manual Passed
	[Tags]  SCP  image  manual  skipped


*** Keywords ***
Generate Random Name
	[Arguments]  ${prefix}  ${suffix_len}
    ${regex}  Generate Random String  ${suffix_len}  [NUMBERS]
    ${name}  Catenate  SEPARATOR=  ${prefix}  ${regex}
    [Return]  ${name}

File Suite Setup
	Base Suite Setup
    Config  master  no aaa accounting exec default start-stop local group tacacs+
    Config  master  no aaa authentication login default group tacacs+ local
	Cli Copy  running-config  snapshot://startup-config
	Cli Copy  running-config  snapshot://startup-config  node=slave

Setup
	Base Test Setup
	Rest Add Tenant  TenantTest12345

Modify Running Config
	${tenant}  Generate Random Name  Tenant  5
	Rest Add Tenant  ${tenant}
	Rest Delete Tenant  TenantTest12345
	auto configure fabric switch   ${spineList}   ${leafList}   2

Teardown
	Run Keyword and Ignore Error  Bash Clear Known Hosts
	Run Keyword and Ignore Error  Cli Copy  snapshot://startup-config  running-config
#	Compare  snapshot://startup-config  running-config
	Run Keyword and Ignore Error  Base Test Teardown

File Suite Teardown
	Run Keyword and Ignore Error  Cli Copy  snapshot://startup-config  running-config
    Config  master  aaa accounting exec default start-stop local
	Run Keyword and Ignore Error  Base Suite Teardown

Compare
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  master  ${first}  ${second}
	${length} =  Get Length  ${status}
	Should Be Equal as Numbers  ${length}  0
	${status} =  Cli Compare  master  ${second}  ${first}
	${length} =  Get Length  ${status}
	Should Be Equal as Numbers  ${length}  0
Compare Slave
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  slave  ${first}  ${second}
	${length} =  Get Length  ${status}
	Should Be Equal as Numbers  ${length}  0
	${status} =  Cli Compare  slave  ${second}  ${first}
	${length} =  Get Length  ${status}
	Should Be Equal as Numbers  ${length}  0

Compare Negative
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  master  ${first}  ${second}
	${length} =  Get Length  ${status}
	Should Not Be Equal as Numbers  ${length}  0
	${status} =  Cli Compare  master  ${second}  ${first}
	${length} =  Get Length  ${status}
	Should Not Be Equal as Numbers  ${length}  0
Compare Negative Slave
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  slave  ${first}  ${second}
	${length} =  Get Length  ${status}
	Should Not Be Equal as Numbers  ${length}  0
	${status} =  Cli Compare  slave  ${second}  ${first}
	${length} =  Get Length  ${status}
	Should Not Be Equal as Numbers  ${length}  0

Compare RC with File Line By Line
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with File Line By Line  ${file}
	Should Be True  ${status}

Compare RC with File Line By Line Negative
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with File Line By Line  ${file}
	Should Not Be True  ${status}

Compare RC with Snapshot Line By Line
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with Snapshot Line By Line  ${file}
	Should Be True  ${status}

Compare RC with Snapshot Line By Line Negative
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with Snapshot Line By Line  ${file}
	Should Not Be True  ${status}

Validate Filename
	[Arguments]  ${file}
	Cli Copy  running-config  ${file}
	Sleep  1
	Compare  running-config  ${file}
	Cli Copy  ${file}  running-config
	Cli Delete  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use:*not*  Cli Delete  ${file}
