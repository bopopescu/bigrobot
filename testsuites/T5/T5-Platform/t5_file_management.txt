*** Settings ***
Documentation  		Testing File Management - copying RC to other locations
Suite Setup  		File Suite Setup
Suite Teardown   	Base Suite Teardown
Test Setup   		Setup
Test Teardown  		Teardown
Force Tags   		Platform  Sanity  File Management
Library  			keywords/BsnCommon.py
Library  			keywords/T5Platform.py
Library  			keywords/T5.py
Library        		String


*** Variables ***
${scp}				scp://recovery@10.197.128.37:scp
${scp RC}			scp://recovery@10.197.128.37:config-RC
${scp file}			scp://recovery@10.197.128.37:config-file
${scp config}		scp://recovery@10.197.128.37:config-config-scp
${scp double}		scp://scp://recovery@10.197.128.37:config-scp
${scp location}		recovery@10.197.128.37:config-scp
${scp inexistent}   scp://recovery@10.197.128.37:inexistent
${scp switch}		scp://recovery@10.197.128.37:scp-switch
${file}				config-file
${config}			config://config-config
${config double}	config://config://config-config
${config name}		config-config
${image}			image://image-file
${image double}		image://image://image-file
@{spineList}		00:00:00:00:00:01:00:01  00:00:00:00:00:01:00:02
@{leafList}			00:00:00:00:00:02:00:01  00:00:00:00:00:02:00:02  00:00:00:00:00:02:00:03  00:00:00:00:00:02:00:04


*** Test Cases ***
T1.1 Copy running config to file
	Cli Copy  running-config  ${file}
	Compare RC with File Line By Line  ${file}
	Compare  running-config  ${file}

T1.2 Copy running config to config://
	Cli Copy  running-config  ${config}
	Compare RC with Config Line By Line  ${config}
	Compare  running-config  ${config}

T1.3 Copy running config to scp://
	Cli Copy  running-config  ${scp}
	Compare  running-config  ${scp}
	[Tags]  SCP


T2.1 Copy running config to file, change RC, verify configs different
	Cli Copy  running-config  ${file}
	Modify Running Config
	Compare RC with File Line By Line Negative  ${file}
	Compare Negative  running-config  ${file}

T2.2 Copy running config to config://, change RC, verify configs different
	Cli Copy  running-config  ${config}
	Modify Running Config
	Compare RC with Config Line By Line Negative  ${config}
	Compare Negative  running-config  ${config}

T2.3 Copy running config to scp://, change RC, verify configs different
	Cli Copy  running-config  ${scp}
	Modify Running Config
	Compare Negative  running-config  ${scp}
	[Tags]  SCP


T3.1 Verify deletion of file
	Cli Copy  running-config  ${file}
	Cli Delete  ${file}

T3.2 Verify deletion of inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: File "*" not found*  Cli Delete  inexistent

T3.3 Verify deletion of config://file
	Cli Copy  running-config  ${config}
	Cli Delete  ${config}

T3.4 Verify deletion of inexistent config://file
	Run Keyword and Expect Error  *Error: Invalid Use: * does not exist*  Cli Delete  config://inexistent


T4.1.1 Copy running config to file, add elements to RC, restore, verify (new elements are kept)
	Rest Add Tenant  TempTenant123
	Cli Copy  running-config  ${file}
	${tenant}  Generate Random Name  Tenant  5
	Rest Add Tenant  ${tenant}
	Cli Copy  running-config  backup
	Rest Delete Tenant  TempTenant123
	Cli Copy  ${file}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  backup
	Should Be True  ${status}
	Compare  running-config  backup
T4.1.2 Copy running config to file, delete elements from RC, restore, verify (deleted elements are restored)
	Cli Copy  running-config  ${file}
	Cli Copy  config://startup-config  running-config
	Cli Copy  running-config  backup
	Cli Copy  ${file}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  backup
	Should Not Be True  ${status}
	Compare Negative  running-config  backup

T4.2 Copy running config to config://, change RC, restore, verify old RC restored
	Cli Copy  running-config  ${config}
	Modify Running Config
	Cli Copy  ${config}  running-config
	Compare RC with Config Line By Line  ${config}
	${status} =  Cli Compare Running Config with Config Line By Line  ${config name}
	Should Be True  ${status}
	Compare  running-config  ${config}

T4.3 Copy running config to scp://, change RC, restore, verify no change in RC
	Cli Copy  running-config  ${scp}
	Modify Running Config
	Cli Copy  running-config  backup
	Cli Copy  ${scp}  running-config
	${status} =  Cli Compare Running Config with File Line By Line  backup
	Should Be True  ${status}
	Compare Negative  running-config  ${scp}
	Compare  running-config  backup
	[Tags]  SCP

T4.4 Copy from scp:// and compare with previous running-config
	Manual Failed
	Cli Copy  running-config  backup
	Cli Copy  ${scp switch}  running-config
	Compare Negative  backup  running-config
	[Tags]  SCP


T5.1 Copy running config to file, copy RC from file to other file, verify
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  file-new
	${status} =  Cli Compare Running Config with File Line By Line  file-new
	Should Be True  ${status}
	${status} =  Cli Compare  running-config  file-new
	Should Be True  ${status}
	${status} =  Cli Compare  file-new  running-config
	Should Be True  ${status}

T5.2 Copy running config to file, copy RC from file to scp://, verify
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp}
	${status} =  Cli Compare  running-config  ${scp}
	Should Be True  ${status}
	${status} =  Cli Compare  ${scp}  running-config
	Should Be True  ${status}

T5.3 Copy running config to config://, copy RC from config:// to file, verify
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: file not config file*  Cli Copy  ${file}  config://config-new

T5.4 Copy running config to config://, copy RC from config:// to scp://, verify
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp}
	Compare  ${config}  ${scp}
	Compare  running-config  ${scp}
	[Tags]  BSC-4955  SCP

T5.5 Copy running config to scp://, copy RC from scp:// to file, verify
	Cli Copy  running-config  ${scp}
	Cli Copy  ${scp}  ${file}
	Compare  running-config  ${file}
	[Tags]  SCP

T5.6.1 Copy running config to scp://, copy RC from scp:// to image, verify
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: Image target must only be image://*  Cli Copy  ${scp}  ${image}
	[Tags]  BSC-4956  SCP
T5.6.2 Copy running config to scp://, copy RC from scp:// to image, verify
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: corrupted image*  Cli Copy  ${scp}  image://
	[Tags]  BSC-4956  SCP

T5.7 Copy running config to config://, copy config:// to scp://, copy from scp:// to file, copy file to config://, copy config:// to RC
	Cli Copy  running-config  ${config}
	Modify Running Config
	Cli Copy  running-config  backup
	Cli Copy  ${config}  ${scp}
	Cli Copy  ${scp}  ${file}
	Cli Copy  ${file}  config://config-new
	Cli Copy  config://config-new  running-config
	Compare  ${config}  running-config
	Compare Negative  backup  running-config
	[Tags]  SCP

T5.8.1 Verify: compare running-config scp://<running-config>
	Cli Copy  running-config  ${scp RC}
	Compare  running-config  ${scp RC}
	[Tags]  SCP

T5.8.2 Verify: compare running-config scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Compare  running-config  ${scp file}
	[Tags]  SCP

T5.8.3 Verify: compare running-config scp://<config://>
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  running-config  ${scp config}
	[Tags]  SCP

T5.8.4 Verify: compare <file> scp://<running-config>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${scp RC}
	Compare  ${file}  ${scp RC}
	[Tags]  SCP

T5.8.5 Verify: compare <file> scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Compare  ${file}  ${scp file}
	[Tags]  SCP

T5.8.6 Verify: compare <file> scp://<config://>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  ${file}  ${scp config}
	[Tags]  SCP

T5.8.7 Verify: compare <config://> scp://<running-config>
	Cli Copy  running-config  ${config}
	Cli Copy  running-config  ${scp RC}
	Compare  ${config}  ${scp RC}
	[Tags]  SCP

T5.8.8 Verify: compare <config://> scp://<file>
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Cli Copy  ${file}  ${scp file}
	Compare  ${config}  ${scp file}
	[Tags]  SCP

T5.8.9 Verify: compare <config://> scp://<config://>
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Compare  ${config}  ${scp config}
	[Tags]  SCP

T5.9.1 Verify: copy scp://<running-config> running-config
	Cli Copy  running-config  ${scp RC}
	Cli Copy  ${scp RC}  running-config
	Compare  ${scp RC}  running-config
	[Tags]  SCP

T5.9.2 Verify: copy scp://<running-config> file
	Cli Copy  running-config  ${scp RC}
	Cli Copy  ${scp RC}  ${file}
	Compare  ${scp RC}  ${file}
	[Tags]  SCP

T5.9.3 Verify: copy scp://<running-config> config://
	Cli Copy  running-config  ${scp RC}
	Run Keyword and Expect Error  *Error: copy: scp://* invalid config file*  Cli Copy  ${scp RC}  ${config}
	[Tags]  SCP

T5.9.4 Verify: copy scp://<file> running-config
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Cli Copy  ${scp file}  running-config
	Compare  ${scp file}  running-config
	[Tags]  SCP

T5.9.5 Verify: copy scp://<file> file
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Cli Copy  ${scp file}  ${file}
	Compare  ${scp file}  ${file}
	[Tags]  SCP

T5.9.6 Verify: copy scp://<file> config://
	Cli Copy  running-config  ${file}
	Cli Copy  ${file}  ${scp file}
	Run Keyword and Expect Error  *Error: copy: scp://* invalid config file*  Cli Copy  ${scp file}  ${config}
	[Tags]  SCP

T5.9.7 Verify: copy scp://<config://> running-config
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  running-config
	Compare  ${scp config}  running-config
	[Tags]  SCP

T5.9.8 Verify: copy scp://<config://> file
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  ${file}
	Compare  ${scp config}  ${file}
	[Tags]  SCP

T5.9.9 Verify: copy scp://<config://> config
	Cli Copy  running-config  ${config}
	Cli Copy  ${config}  ${scp config}
	Cli Copy  ${scp config}  ${config}
	Compare  ${scp config}  ${config}
	[Tags]  SCP


T6.1.1 Verify Filename containing ~
	Validate Filename  test~test
	[Tags]  Filename
T6.1.2 Verify Filename containing !
	Validate Filename  test!test
	[Tags]  Filename
T6.1.3 Verify Filename containing -
	Validate Filename  test-test
	[Tags]  Filename
T6.1.4 Verify Filename containing _
	Validate Filename  test_test
	[Tags]  Filename

T6.2.1 Verify config:// name containing ~
	Validate Filename  config://test~test
	[Tags]  Filename
T6.2.2 Verify config:// name containing !
	Validate Filename  config://test!test
	[Tags]  Filename
T6.2.3 Verify config:// name containing -
	Validate Filename  config://test-test
	[Tags]  Filename
T6.2.4 Verify config:// name containing _
	Validate Filename  config://test_test
	[Tags]  Filename

T6.3 Verify correct processing of special characters in scp:// name (?)
	Cli Copy  running-config  scp://recovery@10.197.128.37:test#test
	Compare  running-config  scp://recovery@10.197.128.37:test#test
	Cli Copy  scp://recovery@10.197.128.37:test#test   running-config
	[Tags]  Filename  SCP

T6.4 Verify empty file name is not allowed
	Run Keyword and Expect Error  *Error: Unexpected end of command*  Cli Copy  running-config  ${EMPTY}
	[Tags]  Filename

T6.5 Verify empty config:// name is not allowed
	Run Keyword and Expect Error  *Error: Invalid Use: copy: config:// requires a filename after slashes*  Cli Copy  running-config  config://
	[Tags]  Filename

T6.6 Verify empty scp:// name is not allowed
	Run Keyword and Expect Error  *Error: Invalid Use: No scp path after scp://*  Cli Copy  running-config  scp://
	[Tags]  Filename  SCP

T6.7 Verify scp://scp:// not correct in copy function
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  running-config  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  running-config
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${file}  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  ${file}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${config}  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  ${config}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Copy  ${scp double}  image://
	[Tags]  BSC-4904  SCP  Filename

T6.8 Verify scp://scp:// not correct in compare function
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Compare  running-config  ${scp double}
	Run Keyword and Expect Error  *ssh: Could not resolve hostname scp: Name or service not known*  Cli Compare  ${scp double}  running-config
	[Tags]  SCP  Filename

T6.9 Verify config://config:// not correct in copy function
	Run Keyword and Expect Error  *Error: Invalid Use: config://config://* must not include / *  Cli Copy  running-config  ${config double}
	Run Keyword and Expect Error  *Error: Invalid Use: config://config://* must not include / *  Cli Copy  ${config double}  running-config
	[Tags]  Filename

T6.10 Verify config://config:// not correct in compare function
	Run Keyword and Expect Error  *Error: Invalid Use: config://config://* must not include / *  Cli Compare  running-config  ${config double}
	Run Keyword and Expect Error  *Error: Invalid Use: config://config://* must not include / *  Cli Compare  ${config double}  running-config
	[Tags]  Filename

T6.11 Verify image-double not correct in copy and compare
	Run Keyword and Expect Error  *Error: Invalid Use: Image target must only be image://*  Cli Copy  ${scp}  image://image://
	[Tags]  BSC-4957  Filename

### and check verifications for empty file names + error messages
T7.1 Verify comparison function between running-config and file
	Cli Copy  running-config  ${file}
	Compare  running-config  ${file}
	Modify Running Config
	Compare Negative  running-config  ${file}

T7.2 Verify comparison function between running-config and config://
	Cli Copy  running-config  ${config}
	Compare  running-config  ${config}
	Modify Running Config
	Compare Negative  running-config  ${config}

T7.3 Verify comparison function between running-config and scp://
	Cli Copy  running-config  ${scp}
	Compare  running-config  ${scp}
	Modify Running Config
	Compare Negative  running-config  ${scp}
	[Tags]  SCP

T7.4 Verify comparison function between file and config://
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${config}
	Compare  ${file}  ${config}
	Modify Running Config
	Cli Copy  running-config  file-new
	Cli Copy  running-config  config://config-new
	Compare Negative  file-new  ${config}
	Compare Negative  ${file}  config://config-new

T7.5 Verify comparison function between file and running-config
	Cli Copy  running-config  ${file}
	Compare  ${file}  running-config
	Modify Running Config
	Compare Negative  ${file}  running-config

T7.6 Verify comparison function between file and scp://
	Cli Copy  running-config  ${file}
	Cli Copy  running-config  ${scp}
	Compare  ${file}  ${scp}
	Modify Running Config
	Cli Copy  running-config  file-new
	Cli Copy  running-config  scp://recovery@10.197.128.37:config-scp-new
	Compare Negative  file-new  ${scp}
	Compare Negative  ${file}  scp://recovery@10.197.128.37:config-scp-new
	[Tags]  SCP

T7.7 Verify comparison function between file and file
	Cli Copy  running-config  file1
	Cli Copy  running-config  file2
	Compare  file1  file2
	Modify Running Config
	Cli Copy  running-config  ${file}
	Compare Negative  file1  ${file}
	Compare Negative  file2  ${file}

T7.8 Verify comparison function between scp:// and config://
	Cli Copy  running-config  ${config}
	Cli Copy  running-config  ${scp}
	Compare  ${config}  ${scp}
	Modify Running Config
	Cli Copy  running-config  config://config-new
	Cli Copy  running-config  scp://recovery@10.197.128.37:config-scp-new
	Compare  config://config-new  scp://recovery@10.197.128.37:config-scp-new
	Compare Negative  config://config-new  ${scp}
	Compare Negative  config://config-new  ${config}
	[Tags]  SCP

T7.9 Verify comparison function between config:// and config://
	Cli Copy  running-config  config://config-new1
	Cli Copy  running-config  config://config-new2
	Compare  config://config-new1  config://config-new2
	Modify Running Config
	Cli Copy  running-config  ${config}
	Compare Negative  config://config-new1  ${config}
	Compare Negative  config://config-new2  ${config}

T7.10 Verify comparison function between running-config and inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  running-config  inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  inexistent  running-config

T7.11 Verify comparison function between running-config and inexistent config://file
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  running-config  config://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  config://inexistent  running-config

T7.12 Verify comparison function between running-config and inexistent scp://file
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  running-config  ${scp inexistent}
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  ${scp inexistent}  running-config
	[Tags]  SCP

T7.13 Verify comparison function between file and inexistent config://file
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  ${file}  config://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  config://inexistent  ${file}

T7.14 Verify comparison function between file and inexistent scp://
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  ${file}  ${scp inexistent}
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  ${scp inexistent}  ${file}
	[Tags]  SCP

T7.15 Verify comparison function between file and inexistent file
	Cli Copy  running-config  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  ${file}  inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  inexistent  ${file}

T7.16 Verify comparison function between inexistent file and inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent1 does not exist*
	...  Cli Compare  inexistent1  inexistent2

T7.17 Verify comparison function between config://file and inexistent file
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  ${config}  inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  inexistent  ${config}

T7.18 Verify comparison function between config://file and inexistent scp://
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  ${config}  ${scp inexistent}
	Run Keyword and Expect Error  *scp: inexistent: No such file or directory*
	...  Cli Compare  ${scp inexistent}  ${config}
	[Tags]  SCP

T7.19 Verify comparison function between config://file and inexistent config://
	Cli Copy  running-config  ${config}
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  ${config}  config://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  config://inexistent  ${config}

T7.20 Verify comparison function between inexistent config://file and inexistent config://
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent1 does not exist*
	...  Cli Compare  config://inexistent1  config://inexistent2

T7.21 Verify comparison function between scp:// and inexistent file
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  ${scp}  inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: inexistent does not exist*
	...  Cli Compare  inexistent  ${scp}
	[Tags]  SCP

T7.22 Verify comparison function between scp:// and inexistent config://file
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  ${scp}  config://inexistent
	Run Keyword and Expect Error  *Error: Invalid Use: config://inexistent does not exist*
	...  Cli Compare  config://inexistent  ${scp}
	[Tags]  SCP

T7.23 Verify comparison function between scp:// and image:// - negative
	Cli Copy  running-config  ${scp}
	Run Keyword and Expect Error  *Error: Invalid Use: unsupported compare scp://* for scp*
	...  Cli Compare  ${scp}  image://
	[Tags]  SCP


T8.1 Verify deletion of file
	Cli Copy  running-config  ${file}
	Cli Delete  ${file}

T8.2 Verify deletion of inexistent file
	Run Keyword and Expect Error  *Error: Invalid Use: File "*" not found*  Cli Delete  inexistent

T8.3 Verify deletion of config://file
	Cli Copy  running-config  ${config}
	Cli Delete  ${config}

T8.4 Verify deletion of inexistent config://file
	Run Keyword and Expect Error  *Error: Invalid Use: * does not exist*  Cli Delete  config://inexistent


T9.1 Verify impossible to copy to invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  running-config  scp://invalid
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  running-config  scp://scp-no-colon@1.2.3.4
	[Tags]  SCP

T9.2 Verify impossible to copy from invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  scp://invalid  running-config
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Cli Copy  scp://scp-no-colon@1.2.3.4  running-config
	[Tags]  SCP

T9.3 Verify impossible to compare with invalid SCP
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Compare  running-config  scp://invalid
	Run Keyword and Expect Error  *Error: Invalid Use: scp path must contain ':'*
	...  Compare  running-config  scp://scp-no-colon@1.2.3.4
	[Tags]  SCP

T9.4 Verify impossible to copy from inexistent SCP
	Run Keyword and Expect Error  *Error: Invalid Use: : No such file or directory*  Cli Copy  ${scp inexistent}  running-config
	[Tags]  SCP

*** Keywords ***
Generate Random Name
	[Arguments]  ${prefix}  ${suffix_len}
    ${regex}  Generate Random String  ${suffix_len}  [NUMBERS]
    ${name}  Catenate  SEPARATOR=  ${prefix}  ${regex}
    [Return]  ${name}

File Suite Setup
	Base Suite Setup
	Bash  master  curl -g -H "Cookie: session_cookie\=$FL_SESSION_COOKIE" localhost:8080/api/v1/data/controller/core/aaa/accounting -X DELETE -d '{"name": "local"}'
	Cli Copy  running-config  config://startup-config
	Cli Copy  running-config  config://startup-config  node=slave

Setup
	Base Test Setup
	Rest Add Tenant  TenantTest12345

Modify Running Config
	${tenant}  Generate Random Name  Tenant  5
	Rest Add Tenant  ${tenant}
	Rest Delete Tenant  TenantTest12345
    auto configure fabric switch   ${spineList}   ${leafList}   2
    Config  master  group test
    Config  master  port-group test
    Config  master  user test

Teardown
	Bash Clear Known Hosts
	Cli Copy  config://startup-config  running-config
	Base Test Teardown

Compare
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  ${first}  ${second}
	Should Be True  ${status}
	${status} =  Cli Compare  ${second}  ${first}
	Should Be True  ${status}

Compare Negative
	[Arguments]  ${first}  ${second}
	${status} =  Cli Compare  ${first}  ${second}
	Should Not Be True  ${status}
	${status} =  Cli Compare  ${second}  ${first}
	Should Not Be True  ${status}

Compare RC with File Line By Line
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with File Line By Line  ${file}
	Should Be True  ${status}

Compare RC with File Line By Line Negative
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with File Line By Line  ${file}
	Should Not Be True  ${status}

Compare RC with Config Line By Line
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with Config Line By Line  ${file}
	Should Be True  ${status}

Compare RC with Config Line By Line Negative
	[Arguments]  ${file}
	${status} =  Cli Compare Running Config with Config Line By Line  ${file}
	Should Not Be True  ${status}

Validate Filename
	[Arguments]  ${file}
	Cli Copy  running-config  ${file}
	Compare  running-config  ${file}
	Cli Copy  ${file}  running-config
	Cli Delete  ${file}
	Run Keyword and Expect Error  *Error: Invalid Use:*not*  Cli Delete  ${file}
