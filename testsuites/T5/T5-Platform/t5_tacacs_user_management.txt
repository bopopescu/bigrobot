*Settings
Documentation  T5 Tacacs Test Suite Release:Iron Horse
Suite Setup  base suite setup
Suite Teardown   base suite teardown
Test Setup   tacacs test setup
Test Teardown   tacacs test teardown
Force Tags   IronHorse  T5  Platform

Library  keywords/T5.py
Library  keywords/T5Platform.py
Library  keywords/AppController.py
Library  keywords/BsnCommon.py
Library  keywords/BigTap.py
Library  Collections.py

* Variable
${projectname}  Big Cloud Fabric Appliance
${version}      2.0.0
    
* Test Case
TC001: Verify Running-Configuration
    Verify Tacacs Configuration  0  10.2.3.201  10
    Verify AAA Authentication Configuration 
    config  c1  show tacacs
    cli  c1  show tacacs                  
    [Tags]  feature

TC002: Verify tacacs server key is not shown in clear text 
    ${myDictionary}=  rest return dictionary from get  /api/v1/data/controller/core/aaa/tacacs?config=true
    ${value}=  Get from dictionary  ${myDictionary[0]}  server
    ${secret}=  Get from dictionary  ${value[0]}  secret
    Should not be equal as strings  ${secret}  secret
    [Tags]  feature
        
TC003: Add user tacacsadmin to group admin and verify access
    verify version  tacacsadmin  adminadmin  ${true}
    [Tags]  feature

TC004: Verify remote authentication with open source tacacs
    ${result}=  rest delete tacacs server  10.2.3.201
    Should be true  ${result}
    ${result}=  rest add tacacs server  10.9.18.2  secret  10
    Should be true  ${result}
    verify version  tacacsadmin  adminadmin  ${true}
    ${result}=  rest delete tacacs server  10.9.18.2    
    Should be true  ${result}
    [Tags]  feature  runthis 

TC005: Verify multiple tacacs servers can be configured 
    ${result}=  rest add tacacs server  10.9.18.2  secret  10
    Should be true  ${result}
    Verify Tacacs Configuration  0  10.2.3.201  10
    Verify Tacacs Configuration  1  10.9.18.2  10
    ${result}=  rest delete tacacs server  10.9.18.2        
    Should be true  ${result}
    [Tags]  feature

TC006: Verify second tacacs server is used when the first is unreachable   
    ${result}=  rest delete tacacs server  10.2.3.201
    Should be true  ${result}
    ${result}=  rest add tacacs server  10.1.18.2  secret  10 
    Should be true  ${result}
    ${result}=  rest add tacacs server  10.2.3.201   secret  10
    Should be true  ${result}
    Verify Tacacs Configuration  0  10.1.18.2  10
    Verify Tacacs Configuration  1  10.2.3.201   10   
    verify version  tacacsadmin  adminadmin  ${true}
    ${result}=  rest delete tacacs server  10.1.18.2
    Should be true  ${result}
    ${result}=  rest delete tacacs server  10.2.3.201 
    Should be true  ${result}
    [Tags]  feature    
            
TC007: Configure mismatched tacacs secret and verify tacacs user cannot login
    ${result}=  rest delete tacacs server  10.2.3.201
    Should be true  ${result}
    ${result}=  rest add tacacs server  10.2.3.201  bsnbsn  10
    Should be true  ${result}
    ${content}=  rest show version  string=version  user=tacacsadmin  password=adminadmin
    Should not be true  ${content}
    [Tags]  feature  

TC008: Verify that when AAA is configured for tacacs only, local AAA should not work even though local user-accounts are configured
    ${result}=  rest add tacacs authentication  tacacs=${true}  tacacs_priority=1  local=${false}  local_priority=2  username=tacacsadmin  password=adminadmin
    verify version  tacacsadmin  adminadmin   ${false}
    ${content}=  rest show version  string=version  user=admin  password=adminadmin   local=${false}
    Should not be true  ${content}            
    rest add tacacs authentication  tacacs=${true}  tacacs_priority=1  local=${true}  local_priority=2  username=tacacsadmin  password=adminadmin
    [Tags]  feature  
                
TC009: Delete tacacs server from configuration and verify tacacs user has no access
    ${result}=  rest delete tacacs server  10.2.3.201
    Should be true  ${result}
    ${content}=  rest show version  string=version  user=tacacsadmin  password=adminadmin
    Should not be true  ${content}
    [Tags]  feature  

TC010: Delete user tacacsadmin from group admin and verify access
    ${result}=  cli delete user from group  tacacsadmin  admin
    Should be true  ${result}
    ${content}=  rest show version  string=version  user=tacacsadmin  password=adminadmin
    Should not be true  ${content}
    ${result}=  rest add user to group  tacacsadmin  admin
    Should be true  ${result}
    [Tags]  feature 
        
* Keywords

tacacs test setup
    ${result}=  rest add tacacs server  10.2.3.201  secret  10
    Should be true  ${result}
    ${result}=  rest add tacacs authentication
    Should be true  ${result}
    ${result}=  rest add user to group  tacacsadmin  admin
    Should be true  ${result}
    
Verify Tacacs Configuration  [Arguments]  ${index}  ${tacacs-server}  ${timeout}
    ${myDictionary}=  rest return dictionary from get  /api/v1/data/controller/core/aaa/tacacs?config=true
    ${value}=  Get from dictionary  ${myDictionary[0]}  server
    ${server-address}=  Get from dictionary  ${value[${index}]}  server-address
    ${timeout}=  Get from dictionary  ${value[${index}]}  timeout
    ${tacacs-timeout}=  Convert to Integer  ${timeout}  
    Should be equal as strings  ${server-address}  ${tacacs-server}
    Should be equal as integers  ${tacacs-timeout}  ${timeout}

Verify AAA Authentication Configuration    
    ${myDictionary}=  rest return dictionary from get  /api/v1/data/controller/core/aaa/authenticator
    ${value}=  Get from dictionary  ${myDictionary[0]}  name
    Should be equal as strings  ${value}  tacacs
    ${priority}=  Get from dictionary  ${myDictionary[0]}  priority
    ${tacacs-priority}=  Convert to Integer  ${priority}
    Should be equal as integers  ${tacacs-priority}  1
    ${value}=  Get from dictionary  ${myDictionary[1]}  name
    Should be equal as strings  ${value}  local
    ${priority}=  Get from dictionary  ${myDictionary[1]}  priority
    ${tacacs-priority}=  Convert to Integer  ${priority}
    Should be equal as integers  ${tacacs-priority}  2

verify version  [Arguments]  ${username}  ${new_password}  ${local}
    ${content}=  rest show version  string=version  user=${username}  password=${new_password}  local=${local}
    Should Contain  ${content}  ${version}
    ${content}=  rest show version  string=name  user=${username}  password=${new_password}  local=${local}
    Should Contain  ${content}  ${projectname}

tacacs test teardown
    ${result}=  cli delete user from group  tacacsadmin  admin
    Should be true  ${result}
    ${result}=  rest delete tacacs server  10.2.3.201
    Should be true  ${result}
    ${result}=  rest delete tacacs authentication      
    Should be true  ${result}
