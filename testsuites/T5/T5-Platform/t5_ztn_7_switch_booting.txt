*** Settings ***
Documentation       Testing ZTN
...              The purpose of this test suite is to check managing switches with ZTN. Small testbed will be used.
...
...              *Test topology*: pair of controllers, one spine switch, two leaf switches (Accton).
...
...              For tests which check correct installation of flows on the switches, simple forwarding configuration
...              and small traffic rate will be used.
...              Larger testbeds will be tested inherently in other testsuites.
...
...              In this Test Suite the following terms will be used:
...
...              * stand-by switch - switch configured on the controller (with MAC address) with no fabric-role specified
...
...              * provisioned switch - switch configured on the controller (with MAC address) with fabric role, forwarding traffic
...
...              * suspended switch - switch connected to fabric but not configured on the controller,
...                or running wrong version of Swith Light, or with incorrect running-config
...
...              * ztn-discovery mode - switch in the phase of booting/searching for ZTN servers with relevant manifest file
...
...              * ZTN configuration, which includes:
...
...                - configuration mirrored from controller's configuration to switches (NTP, Logging, SNMP)
...
...                - controllers' IP addresses
...
...                  - per-switch hostname (as switch name on the controller)
...
...                  - per-switch interface configuration (as assigned by DHCP server when switch booted)
...
...              * ZTN configuration change:
...
...                - any controller Cli change that affects the switch startup-config
...
...              * Switch mode change:
...
...                - any controller Cli change that affects the switch provisioning (fabric role, MAC)

Suite Setup		Suite Setup
Suite Teardown		Suite Teardown
Test Setup		Test Setup
Test Teardown	Test Teardown
Force Tags		T5  ZTN  Feature  IronHorse
Library  		keywords/BsnCommon.py
Library		keywords/T5Platform.py
Library		keywords/T5Utilities.py
Library		keywords/T5ZTN.py
Library		String
Resource		t5_ztn_resource.txt


*** Test Cases ***
#7.26-28: behavior is undefined, it is timing dependent; in all cases the switch should be visible from the controller Cli and should be rebootable to stabilize the fabric
#7.26. Verify following behaviors when switch's MAC gets deleted during switch boot up
T7.26.1 - while Installer download - no SWI download
	Cli Run  master  enable; config
	Cli Run  master  system reinstall switch ${s1_alias} reboot factory-default
	Sleep  20
	Telnet Verify Onie Discovery Succeeded  s1
	Cli Run  master  switch ${s1_alias}; no mac ${s1_mac}
	Sleep  100
	Telnet Verify ZTN Discovery Failed  s1
	Telnet Verify ZTN Discovery Failed  s1
	Verify Switch Connection State  False  ${s1_alias}
	Verify Switch Fabric Connection State  not_connected  ${s1_alias}
    [Tags]  Sanity

T7.26.2.1 - while SWI download - startup-config not cached - switch does not boot
    Telnet Delete ZTN Cache  all  s2
    Cli Reboot Switch  master  ${s2_mac}
    Telnet Wait For Switch To Find Manifest  s2
    Cli Run  master  switch ${s2_alias}; no mac ${s2_mac}
    Sleep  60
    Telnet Verify ZTN Discovery Failed  s2
    Telnet Verify ZTN Discovery Failed  s2
    Verify Switch Connection State  False  ${s2_alias}
    Verify Switch Fabric Connection State  not_connected  ${s2_alias}
    [Tags]  Sanity

T7.26.2.2 - while SWI download - startup-config cached - switch undefined
    Telnet Delete ZTN Cache  swi  s3
    Cli Reboot Switch  master  ${s3_mac}
    Sleep  30
    Telnet Wait For Switch To Find Manifest  s3
    Cli Run  master  switch ${s3_alias}; no mac ${s3_mac}
    Sleep  150
    Verify Switch Connection State  False  ${s3_alias}
    Cli Run and Verify Output  master
    ...  show switch
    ...  ${s3_ip} suspended (No fabric role configured)

T7.26.3 - while image startup - switch rebooted (starts up, gets removed)
    Cli Reboot Switch  master  ${s4_mac}
    Sleep  30
    Telnet Wait For Switch To Start Booting  s4
    Cli Run  master  switch ${s4_alias}; no mac ${s4_mac}
    Sleep  150
    Verify Switch Connection State  False  ${s4_alias}
    Cli Run and Verify Output  master
    ...  show switch
    ...  ${s4_ip} suspended (No fabric role configured)


#7.27. Verify following behaviors when switch's name is changed during switch boot up
T7.27.1 - while Installer download - correct SWI download and fabric join
    Cli Run  master  enable; config
    Cli Run  master  system reinstall switch ${s5_alias} reboot factory-default
    Sleep  20
    Telnet Verify Onie Discovery Succeeded  s5
    Cli Run  master  switch ${s5_alias}; no mac ${s5_mac}
    Cli Run  master  switch temp-name; mac ${s5_mac}; fabric-role spine
    Sleep  120
    Telnet Wait For Switch To Reload  s5
    Sleep  60
    Verify Switch Startup Config  ${s5_mac}  temp-name
    Verify Switch Running Config  ${s5_mac}  ${s5_alias}
    Verify Switch In Provisioned Mode  temp-name

T7.27.2.1 - while SWI download - startup-config not cached - switch boots properly
    Telnet Delete ZTN Cache  all  s1
    Cli Reboot Switch  master  ${s1_mac}
    Telnet Wait For Switch To Find Manifest  s1
    Cli Run  master  switch ${s1_alias}; no mac ${s1_mac}
    Cli Run  master  switch temp-name; mac ${s1_mac}; fabric-role spine
    Sleep  220
    Telnet Wait For Switch To Reload  s1
    Sleep  30
    Verify Switch Startup Config  ${s1_mac}  temp-name
    Verify Switch Running Config  ${s1_mac}  ${s1_alias}
    Verify Switch In Provisioned Mode  temp-name

T7.27.2.2 - while SWI download - startup-config cached - switch undefined
    Telnet Delete ZTN Cache  all  s2
    Cli Reboot Switch  master  ${s2_mac}
    Telnet Wait For Switch To Find Manifest  s2
    Cli Run  master  switch ${s2_alias}; no mac ${s2_mac}
    Cli Run  master  switch temp-name; mac ${s2_mac}; fabric-role leaf
    Sleep  200
    Telnet Wait For Switch To Reload  s2
    Sleep  60
    Verify Switch Startup Config  ${s2_mac}  temp-name
    Verify Switch Running Config  ${s2_mac}  ${s2_alias}
    Verify Switch In Provisioned Mode  temp-name

T7.27.3 - while image startup - reload indicated
    Cli Reboot Switch  master  ${s3_mac}
    Sleep  30
    Telnet Wait For Switch To Start Booting  s3
    Cli Run  master  switch ${s3_alias}; no mac ${s3_mac}
    Cli Run  master  switch temp-name; mac ${s3_mac}; fabric-role leaf
    Sleep  120
    Verify Switch Startup Config  ${s3_mac}  temp-name
    Verify Switch Running Config  ${s3_mac}  ${s3_alias}
    Verify Switch In Provisioned Mode  temp-name

#7.28. Verify following behaviors when ZTN config is changed during switch boot up
T7.28.1 - while Installer download - correct SWI download and fabric join
    Cli Run  master  enable; config
    Cli Run  master  system reinstall switch ${s4_alias} reboot
    Sleep  20
    Telnet Verify Onie Discovery Succeeded  s4
    Randomize ZTN Configuration
    Sleep  100
    Telnet Wait For Switch To Reload  s4
    Sleep  30
    Verify Switch Startup Config  ${s4_mac}  ${s4_alias}
    Verify Switch Running Config  ${s4_mac}  ${s4_alias}
    Verify Switch In Provisioned Mode  ${s4_alias}

T7.28.2 - while SWI download - switch undefined (suspended)
	Telnet Reboot Switch  s1
	Telnet Wait For Switch To Find Manifest  s1
	Randomize ZTN Configuration
	Telnet Wait For Switch To Reload  s1
	Sleep  60
	Verify Switch Startup Config  ${s1_mac}  ${s1_alias}
	Verify Switch Running Config  ${s1_mac}  ${s1_alias}
	Verify Switch In Provisioned Mode  ${s1_alias}

T7.28.3 - while image startup - switch undefined (suspended)
	Telnet Reboot Switch  s2
	Telnet Wait For Switch To Start Booting  s2
	Randomize ZTN Configuration
	Telnet Wait For Switch To Reload  s2
	Sleep  60
	Verify Switch Startup Config  ${s2_mac}  ${s2_alias}
	Verify Switch Running Config  ${s2_mac}  ${s2_alias}
	Verify Switch In Provisioned Mode  ${s2_alias}

#7.29. Verify following behaviors when switch mode (stand-by <-> provisioned) is changed during switch boot up
T7.29.1 - while Installer download - correct SWI download and fabric join
    Cli Run  master  enable; config
    Cli Run  master  system reinstall switch ${s3_alias} reboot
    Sleep  20
    Telnet Verify Onie Discovery Succeeded  s3
    Set All Switches Mode Standby
    Sleep  150
    Telnet Wait For Switch To Reload  s3
    Sleep  30
    Verify All Switches In Standby Mode
    Verify All Switches Startup Config
    Verify All Switches Running Config

T7.29.2 - while SWI download - switch undefined (suspended)
	Cli Reboot Switch  master  all
	Telnet Wait For Switch To Find Manifest  s1
	Set All Switches Mode Standby
	Telnet Wait For Switch To Reload  s1
	Sleep  100
	Verify All Switches In Standby Mode
	Verify All Switches Startup Config
	Verify All Switches Running Config

T7.29.3 - while image startup - switch undefined (suspended)
	Cli Reboot Switch  master  all
	Telnet Wait For Switch To Start Booting  s5
	Set All Switches Mode Standby
	Telnet Wait For Switch To Reload  s5
	Sleep  100
	Verify All Switches In Standby Mode
	Verify All Switches Startup Config
	Verify All Switches Running Config

T7.30. Check what happens when switch's flash is full and it is supposed to download new SL image
	Manual Passed
	[Tags]  manual
