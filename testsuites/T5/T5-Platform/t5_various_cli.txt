*** Settings ***
Documentation  		Various CLI commands
Suite Setup  		Various Suite Setup
Suite Teardown		Various Suite Teardown
Test Setup   		Setup
Test Teardown  		Teardown
Force Tags   		Sanity  Platform  CLI  feature  T5  IronHorse
Library  			keywords/BsnCommon.py
Library  			keywords/T5Platform.py
Library  			keywords/T5Utilities.py
Library  			keywords/T5.py
Library        		String

*** Variables ***

*** Test Cases ***
T1 Show running-config
	Cli Run  master  show running-config

T2.1 No Show This in config mode
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no show this
	[Tags]  show
T2.2 No Show This in config-switch submode
	Cli Run  master  switch test
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no show this
	[Tags]  BSC-5042  show
T2.3 No Show This in config-tenant submode
	Cli Run  master  tenant test
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no show this
	[Tags]  BSC-5042  show
T2.4 No Show This in config-controller submode
	Cli Run  master  controller
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no show this
	[Tags]  BSC-5042  show

T3 "no snmp-server community ro <foo>" in config-switch submode
	Cli Run  master  snmp-server community ro test
	Cli Run  master  switch test
	Cli Run  master  no snmp-server community ro test
	Cli Run and Verify Output  master
	...  show run snmp
	...  snmp-server community ro test
	...  flag=False
	[Tags]  BSC-5039  show  snmp

T4 Monitor session string as identifier
	Run Keyword and Expect Error
	...  *Error: Invalid argument: range: not an integer:*r*
	...  Cli Run  master  monitor session r
	[Tags]  MonitorSession  BSC-5012

T5 Monitor session source tcp string
	Cli Run  master  monitor session 1
	Run Keyword and Expect Error
	...  *Cannot specify a packet match criteria*if direction is not ingress*
	...  Cli Run  master  source switch test interface eth1 ip-proto tcp
	[Tags]  MonitorSession  BSC-5017

T6 Monitor session destination interface numerical only
	Cli Run  master  monitor session 1
	Run Keyword and Expect Error
	...  *Invalid argument: Invalid Interface, e.g. ethernet1, ethernet1/1: 3; Invalid characters in interface*
	...  Cli Run  master  destination switch test interface 3
	[Tags]  MonitorSession  BSC-5017

T7 Autocompletions for user full name
	Cli Run  master  user test
	Cli Run and Verify Output  master
	...  full-name ? 'test user'
	...  <full-name>
	Cli Run and Verify Output  master
	...  full-name ? 'test user'
	...  r'Default admin'
	...  flag=False
	[Tags]  BSC-5015  UserManagement

T8 Escaping quote and double quote strings - user full-name
	Cli Run  master  user test
	Cli Run  master  full-name "test test"
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name 'test test'
	Cli Run  master  full-name 'test test'
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name 'test test'
	Cli Run  master  full-name test
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name test
	Cli Run  master  full-name 'test\\n'
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name 'test\\n'\r
	Cli Run  master  full-name \"test\\r\\n\'\'\'\"
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name \"test\\r\\n\'\'\'\"
	Cli Run  master  full-name "'''"
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name "'''"
	Cli Run  master  full-name '"""'
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name '"""'
	Cli Run  master  full-name "''''"
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name "''''"
	Cli Run  master  full-name '""""'
	Cli Run and Verify Output  master
	...  show running-config user
	...  full-name '""""'
	[Tags]  BSC-5010  BSC-5011

T9 Escaping quote and double quote strings - switch name
	Run Keyword and Expect Error
	...  *Error: Unbalanced Quote*
	...  Cli Run  master  switch test"test
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid Alphabetic character, followed by alphanumerics: 'test"test'; Invalid characters in identifier*
	...  Cli Run  master  switch 'test"test'
	Run Keyword and Expect Error
	...  *Error: Unbalanced Quote*
	...  Cli Run  master  switch test'test

T10 Escaping quote and double quote strings - user name
	Run Keyword and Expect Error
	...  *Error: Unbalanced Quote*
	...  Cli Run  master  user test"test
	Run Keyword and Expect Error
	...  *Error: Unbalanced Quote*
	...  Cli Run  master  user test'test
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid Alphabetic character, followed by alphanumerics: 'test"test'; Invalid characters in identifier*
	...  Cli Run  master  user 'test"test'
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid Alphabetic character, followed by alphanumerics: "test''test"; Invalid characters in identifier*
	...  Cli Run  master  user "test''test"

T11 User \n via REST
        Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  a\n
	Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  a\naa
	Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  \\
	Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  \r
	Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  rrse"
	Run Keyword and Expect Error
        ...  TestError: REST call failed with status code 400
	...  My Rest Add User  \n\r
	[Tags]  BSC-5051  REST  Skipped

T12.1 show this in config-controller submode
	Cli Run  master  controller
	Cli Run  master  show this
	[Tags]  BSC-5061  BSC-5491
T12.2 show this in config-switch-if submode
	Cli Run  master  switch test
	Cli Run  master  mac 12:22:33:44:55:66
	Cli Run  master  interface eth1
	Cli Run  master  show this
	[Tags]  BSC-5135  BVS-1463

T13 Characters \n and \r in identifiers
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid Alphabetic * Invalid characters in identifier*
	...  Cli Run  master  user 'a\\n'
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid Alphabetic * Invalid characters in identifier*
	...  Cli Run  master  user 'a\\r'
	Run Keyword and Expect Error
	...  *Error: Unbalanced Quote*
	...  Cli Run  master  switch 'a\\n\\n'b'
	Run Keyword and Expect Error
	...  *Error: Invalid argument: syntax: expected [0-9a-zA-Z][-.0-9a-zA-Z_]*
	...  Cli Run  master  tenant 'a\\n\\r\\n \\r\\n'
	[Tags]  BSC-5051  BSC-5011


T14 a\n in banner
	Cli Run  master  banner 'a\\n'
	Cli Run and Verify Output  master
	...  show running-config banner
	...  banner 'a\\n'
	Cli Run and Verify Output  master
	...  show banner
	...  Banner : a\r
	[Tags]  banner  BSC-5366

T15 no tacacs server timeout
	Cli Run  master  tacacs server timeout 10
	Cli Run  master  no tacacs server timeout 5
	Cli Run  master  tacacs server timeout 10
	Cli Run  master  no tacacs server timeout
	[Tags]  BSC-6651  AAA

T16 Switch commands in config-switch-if submode
	Cli Run and Verify Output  master
	...  show running-config switch
	...  switch test
	...  flag=False
	Cli Run  master  switch test
	Cli Run  master  interface eth1
	Cli Run  master  fabric-role spine
	Cli Run and Verify Output  master
	...  show running-config switch test
	...  fabric-role spine
	Cli Run  master  fabric-role leaf
	Cli Run and Verify Output  master
	...  show running-config switch test
	...  fabric-role leaf
	Cli Run and Verify Output  master
	...  show running-config switch test
	...  fabric-role spine
	...  flag=False
	Cli Run  master  mac 22:11:11:11:11:11
	Cli Run and Verify Output  master
	...  show running-config switch test
	...  mac 22:11:11:11:11:11
	Cli Run  master  leaf-group test
	Cli Run and Verify Output  master
	...  show running-config switch test
	...  leaf-group test
	[Tags]  BSC-5067

T17 DNS configuration
	Cli Run  master  local node
	Cli Run  master  interface ethernet0
	Cli Run  master  dns search google.com
	Run Keyword and Expect Error
	...  *Error: Invalid argument: Invalid IP address (dotted quad): 8.8.8.88888; invalid string pattern*
	...  Cli Run  master  dns server 8.8.8.88888
	Cli Run  master  dns server 8.8.8.8
	Sleep  5 sec
	Log  Checking changes in local-config
	Cli Run and Verify Output  master
	...  show running-config local
	...  8.8.8.8
	Cli Run and Verify Output  master
	...  show running-config local
	...  google.com
	Log  Checking changes in bash NetworkManager
	Cli Run  master  debug bash
	Cli Run and Verify Output  master
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  8.8.8.8
	Cli Run and Verify Output  master
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  google.com
	Cli Run  master  exit
	Log  Checking there are no changes on Slave
	Cli Run and Verify Output  slave
	...  show running-config local
	...  8.8.8.8
	...  flag=False
	Cli Run and Verify Output  slave
	...  show running-config local
	...  google.com
	...  flag=False
	Cli Run  slave  debug bash
	Cli Run and Verify Output  slave
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  8.8.8.8
	...  flag=False
	Cli Run and Verify Output  slave
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  google.com
	...  flag=False

	Cli Run  master  no dns search google.com
	Cli Run  master  no dns server 8.8.8.8
	Sleep  5 sec
	Log  Checking changes in local-config
	Cli Run and Verify Output  master
	...  show running-config local
	...  8.8.8.8
	...  flag=False
	Cli Run and Verify Output  master
	...  show running-config local
	...  google.com
	...  flag=False
	Log  Checking changes in bash NetworkManager
	Cli Run  master  debug bash
	Cli Run and Verify Output  master
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  8.8.8.8
	...  flag=False
	Cli Run and Verify Output  master
	...  sudo cat /etc/NetworkManager/system-connections/eth0
	...  google.com
	...  flag=False
	Cli Run  master  exit
	[Tags]  DNS  BSC-5060

T18 'no stats' command
	Cli Run  master  tenant test
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no stats vns interval 15
	Cli Run and Verify Output  master
	...  show run tenant
	...  tenant test
	Cli Run  master  monitor session 1
	Run Keyword and Expect Error
	...  *Error: no matching commands*
	...  Cli Run  master  no stats interface interval 15
	Cli Run and Verify Output  master
	...  show run monitor
	...  monitor session 1
	[Tags]  BSC-5043

T19 'show this' in -vns and -endpoint submodes
	Cli Run  master  tenant A
	Cli Run  master  router
	Cli Run  master  vns A1
	Cli Run  master  member-port-group rack0pg1 vlan untagged
	Cli Run  master  member-port-group rack1pg1 vlan untagged
	Cli Run  master  endpoint host1
	Cli Run  master  ip 10.0.0.1
	Cli Run  master  vns A2
	Cli Run  master  member-port-group rack0pg2 vlan untagged
	Cli Run  master  member-port-group rack1pg2 vlan untagged
	Cli Run  master  config
	Cli Run  master  tenant A
	Cli Run  master  vns A1
	Cli Run and Verify Output  master
	...  show this
	...  vns A2
	...  flag=False
	Cli Run  master  endpoint host1
	Cli Run and Verify Output  master
	...  show this
	...  vns A2
	...  flag=False
	[Tags]  BSC-5085  Skipped

T20.1 AAA Accounting round-robin support
	Cli Run  master  tacacs server host 1.2.3.4 timeout 5 key secret
	Cli Run  master  tacacs server host 10.192.64.1 timeout 5 key secret
	Cli Run  master  tacacs server host 10.192.3.1 timeout 5 key secret
	Cli Run  master  tacacs server host 10.192.4.91 timeout 5 key secret
	Cli Run  master  aaa accounting exec default start-stop group tacacs+  cmd_timeout=60
	Cli Run  master  config
	Cli Run  master  tenant test
	Cli Run  master  tacacs server host 10.197.128.32 timeout 5 key secret
	Cli Run  master  no tacacs server host 10.192.4.91 timeout 5 key secret  cmd_timeout=30
	Cli Run  master  config
	Cli Run  master  tenant test1
	Cli Run  master  no aaa accounting exec default start-stop group tacacs+  cmd_timeout=30
	[Tags]  BSC-5019  AAA  TACACS  skipped
T20.2 TACACS Default timeout
	Cli Run  master  tacacs server timeout 10
	Cli Run  master  tacacs server host 5.5.5.5 timeout 5 key secret
	Cli Run  master  tacacs server host 1.2.3.4
	Cli Run  master  aaa accounting exec default start-stop group tacacs+  cmd_timeout=31
	Cli Run  master  no aaa accounting exec default start-stop group tacacs+  cmd_timeout=46
	Cli Run  master  no tacacs server timeout 10
	Cli Run  master  aaa accounting exec default start-stop group tacacs+  cmd_timeout=71
	Cli Run  master  no aaa accounting exec default start-stop group tacacs+  cmd_timeout=141
	[Tags]  BSC-5088  AAA  TACACS  skipped

T21 Escaping special characters in quoted strings
	Cli Run  master  snmp-server contact tests\\n_\\r_\\n_
	Cli Run and Verify Output  master
	...  show run snmp
	...  snmp-server contact tests\\n_\\r_\\n_
	Cli Run  master  snmp-server location Location_\\n_\\r_\\n_
	Cli Run and Verify Output  master
	...  show run snmp
	...  snmp-server location Location_\\n_\\r_\\n_
	[Tags]  BSC-5011

T22 Localhost helptext
	Cli Run and Verify Output  master
	...  show controller ?
	...  Show controller stats
	[Tags]  BSC-5742

T23 show controller localhost
	Cli Run and Verify Output  master
	...  show controller localhost
	...  Cpu user Percentage
	[Tags]  BSC-5863

T24 Chinese characters in commands
	Cli Run  master  banner \\0xe4\\0xbd\\0xa0\\0xe5\\0xa5\\0xbd
	Cli Run and Verify Output  master
	...  show banner
	...  \\0xe4\\0xbd\\0xa0\\0xe5\\0xa5\\0xbd
	[Tags]  banner  BSC-5366  BSC-5102

T25 Polish characters in commands
	Cli Run  master  snmp-server location Ko\\0xc5\\0x9bciuszki_34,_\\0xc5\\0x81\\0xc3\\0xb3d\\0xc5\\0xba
	Cli Run and Verify Output  master
	...  show running-config snmp
	...  snmp-server location Ko\\0xc5\\0x9bciuszki_34,_\\0xc5\\0x81\\0xc3\\0xb3d\\0xc5\\0xba
	[Tags]  snmp  SpecialCharacters

T26 Route and Router help texts
	Cli Run  master  tenant test
	Cli Run  master  logical-router
	Cli Run and Verify Output  master
	...  route? 0.0.0.0/32 next-hop tenant-test
	...  route Add an IP subnet route on the logical router
	Cli Run and Verify Output  master
	...  logical-route?r
	...  Enter the logical router configuration submode

T27 Interaction between snmp and banner commands
	Cli Run  master  snmp-server location Loc1
	Cli Run and Verify Output  master
	...  show running-config snmp
	...  snmp-server location Loc1
	Cli Run  master  banner Welcome
	Cli Run and Verify Output  master
	...  show running-config snmp
	...  snmp-server location Loc1
	Cli Run and Verify Output  master
	...  show running-config banner
	...  banner Welcome
	[Tags]  banner  snmp  BSC-5141

T28 NTP sync command
	Cli Run  master  ntp server 0.bigswitch.pool.ntp.org
	Cli Run  master  ntp sync  cmd_timeout=25
	Cli Run  master  ntp sync 0.bigswitch.pool.ntp.org  cmd_timeout=25
	[Tags]  NTP  BSC-5169

T29 Deletion of ntp server
	Run Keyword and Expect Error
	...  *Error*
	...  Cli Run  master  no ntp server 0.bigswitch.pool.ntp.org
	Cli Run and Verify Output  master
	...  show running-config ntp
	...  ntp server 0.bigswitch.pool.ntp.org
	[Tags]  Skipped  BSC-5095

T30 No fabric role
	Cli Run  master  switch test
	Run Keyword and Expect Error
	...  *Error: Unexpected end of command*
	...  Cli Run  master  no fabric-role
	Cli Run  master  no fabric-role spine
	Cli Run  master  no fabric-role leaf
	[Tags]  BVS-1482

T31 Validation of "monitor session" configuration
	Cli Run  master  monitor session 1
	Cli Run  master  source switch test interface ethernet1
	Run Keyword and Expect Error
	...  *Error: Validation failed: [Validate Session 1]: Destination test/ethernet1 is already configured as a source in session [1]*
	...  Cli Run  master  destination switch test interface ethernet1
	[Tags]  BVS-1219

T32 Show running config when cookie session expired
	${session_id} =  Cli Get Session Hash
	${name}=    BsnCommon.get node name    master
	Cli Run  ${name}  enable
	Cli Run  ${name}  config
	Run Keyword and Ignore Error  Cli Run  ${name}  clear session session-id ${session_id}
	Cli Run and Verify Output  master
	...  show running-config
	...  Warning: Running config incomplete due to expired authentication token
	[Tags]  BSC-5343  session

T33 Show banner simple test
	Cli Run  master  banner 'This is test banner'
	Cli Run and Verify Output  master
	...  show running-config banner
	...  banner 'This is test banner'
	Cli Run and Verify Output  master
	...  show banner
	...  This is test banner
	[Tags]  banner  BSC-5366

T34 Rebooting dummy switch
	Cli Run  master  switch test
	Cli Run  master  mac 12:22:33:44:55:66
	Cli Run  master  system reboot switch test
	Run Keyword and Expect Error
	...  *Error: Invalid Use: reboot only supported using IP Address for disconnected switches*
	...  Cli Run  master  system reboot switch 12:22:33:44:55:66
	[Tags]  ZTN  BSC-5569

#T35 Terminal length
#	Cli Run  master  enable;config;set length 10
#	Cli Run and Verify Output Length
#	...  master
#	...  show run
#	...  10
#	Cli Run  master  enable;config;set length 0

T36 Tenant names
	Verify Regular Name  tenant
T37 Segment names
	Cli Run  master  tenant test
	Verify Regular Name  segment
T38 User names
	Verify Regular Name  user
T39 Port-group names
	Verify Regular Name  port-group
T40 Switch names
    Verify Name  switch  -
    Verify Name  switch  .
T41 Endpoint names
	Cli Run  master  tenant test; segment test
	Verify Regular Name  endpoint
T42 Policy-list names
	Cli Run  master  tenant test; logical-router
	Verify Regular Name  policy-list
T43 Next-hop-group names
	Cli Run  master  tenant test; logical-router
	Verify Regular Name  next-hop-group

T44 no tacacs server host 10.2.3.201 timeout 10 key secret
    Cli Run  master  tacacs server host 10.2.3.201 timeout 10 key secret
    Cli Run and Verify Output  master
    ...  show running-config tacacs
    ...  tacacs server host 10.2.3.201 timeout 10 key
    Cli Run  master  no tacacs server host 10.2.3.201 timeout 10 key secret
    Cli Run and Verify Output  master
    ...  show running-config tacacs
    ...  tacacs server host 10.2.3.201
    Cli Run  master  no tacacs server host 10.2.3.201
    Cli Run and Verify Output  master
    ...  show running-config tacacs
    ...  tacacs server host 10.2.3.201
    ...  flag=False
    [Tags]  AAA  BSC-6596

T45 snmp-server switch trap fan-status
    Run Keyword and Expect Error
    ...  *Error: Invalid argument: range: expected [10..100000]*
    ...  Cli Run  master  snmp-server switch trap fan-status 1
    Run Keyword and Expect Error
    ...  *Error: Invalid argument: range: expected [10..100000]*
    ...  Cli Run  master  snmp-server switch trap fan-status 100000000
    [Tags]  SNMP  ZTN  BSC-6530

T46 system reboot switch 22:22:22:22:22:22
    Run Keyword and Expect Error
    ...  *Error: Invalid Use: reboot only supported using IP Address for disconnected switches*
    ...  Cli Run  master  system reboot switch 22:22:22:22:22:22
    [Tags]  ZTN  BSC-6304

T47 copy running-config foo
    Run Keyword and Expect Error
    ...  *Error: Invalid Use: dest foo not supported*
    ...  Cli Run  master  copy running-config foo
    Run Keyword and Expect Error
    ...  *Error: Invalid Use: source foo not supported *
    ...  Cli Run  master  copy foo running-config
    [Tags]  running-config  BSC-6229

T48 show switch foo inventory
    Run Keyword and Expect Error
    ...  *Error: Invalid Use: unsupported action: "show" for unknown/unmanaged switch: "foo"*
    ...  Cli Run  master  show switch foo inventory
    [Tags]  ZTN  PAN-1189

T49 no logging enable switch-remote
    Cli Run  master  logging switch-remote
    Cli Run and Verify Output  master
    ...  show running-config logging
    ...  logging switch-remote
    Cli Run  master  no logging switch-remote
    Cli Run and Verify Output  master
    ...  show running-config logging
    ...  logging switch-remote
    ...  flag=False
    [Tags]  ZTN  BSC-6089  logging

T50 route 0.0.0.0/32 next-hop null
    Cli Run  master  tenant test; logical-router
    Cli Run  master  route 0.0.0.0/32 next-hop null
    Cli Run and Verify Output  master
    ...  show running-config tenant
    ...  route 0.0.0.0/32 next-hop null
    [Tags]  BVS-2036

T51.1 Show logging syslog last master
    ${node}  Set Variable  master
    ${threshold}  Set Variable  20
    Config  master  no ntp time-zone
    Sleep  3
    ${result1}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone America/New_York
    Sleep  3
    ${result2}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone America/Los_Angeles
    Sleep  3
    ${result3}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone Asia/Tokyo
    Sleep  3
    ${result4}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone Europe/Warsaw
    Sleep  3
    ${result5}  Cli Get Number Of Logs In Last  ${node}  5hour

    ${status}=  Evaluate  (${result2} - ${result1}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result3} - ${result2}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result4} - ${result3}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result5} - ${result4}) < ${threshold}
    Should Be True  ${status}
    [Tags]  BSC-7011

T51.2 Show logging syslog last slave
    ${node}  Set Variable  slave
    ${threshold}  Set Variable  20
    Cli Run  slave  exit
    Cli Run  slave  enable
    Cli Run  slave  show run
    Cli Run  master  no ntp time-zone
    Sleep  3
    ${result1}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone America/New_York
    Sleep  3
    ${result2}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone America/Los_Angeles
    Sleep  3
    ${result3}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone Asia/Tokyo
    Sleep  3
    ${result4}  Cli Get Number Of Logs In Last  ${node}  5hour
    Config  master  ntp time-zone Europe/Warsaw
    Sleep  3
    ${result5}  Cli Get Number Of Logs In Last  ${node}  5hour

    ${status}=  Evaluate  (${result2} - ${result1}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result3} - ${result2}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result4} - ${result3}) < ${threshold}
    Should Be True  ${status}
    ${status}=  Evaluate  (${result5} - ${result4}) < ${threshold}
    Should Be True  ${status}
    [Tags]  BSC-7011

*** Keywords ***
Generate Random Name
	[Arguments]  ${prefix}  ${suffix_len}
    ${regex}  Generate Random String  ${suffix_len}  [NUMBERS]
    ${name}  Catenate  SEPARATOR=  ${prefix}  ${regex}
    [Return]  ${name}

Verify Name
	[Arguments]  ${name}  ${character}
	Cli Run  master  ${name} 'abc${character}ttt'
	Cli Run and Verify Output
	...  master
	...  show run ${name}
	...  abc${character}ttt

Verify Name Negative
	[Arguments]  ${name}  ${character}
	Run Keyword and Expect Error  *  Cli Run  master  ${name} 'abc${character}ttt'


Verify Regular Name
	[Arguments]  ${name}
    Verify Name  ${name}  -
    Verify Name  ${name}  _
    Verify Name  ${name}  .
    Verify Name  ${name}  1
    Verify Name  ${name}  2
    Verify Name  ${name}  3
    Verify Name  ${name}  4
    Verify Name  ${name}  5
    Verify Name  ${name}  6
    Verify Name  ${name}  7
    Verify Name  ${name}  8
    Verify Name  ${name}  9
    Verify Name  ${name}  0
    Verify Name Negative  ${name}  ~
    Verify Name Negative  ${name}  !
    Verify Name Negative  ${name}  @
    Verify Name Negative  ${name}  \#
    Verify Name Negative  ${name}  $
    Verify Name Negative  ${name}  %
    Verify Name Negative  ${name}  ^
    Verify Name Negative  ${name}  &
    Verify Name Negative  ${name}  *
    Verify Name Negative  ${name}  (
    Verify Name Negative  ${name}  )
    Verify Name Negative  ${name}  +
    Verify Name Negative  ${name}  `
    Verify Name Negative  ${name}  =
    Verify Name Negative  ${name}  [
    Verify Name Negative  ${name}  ]
    Verify Name Negative  ${name}  \\
    Verify Name Negative  ${name}  ;
    Verify Name Negative  ${name}  '
    Verify Name Negative  ${name}  ,
    Verify Name Negative  ${name}  /
    Verify Name Negative  ${name}  <
    Verify Name Negative  ${name}  >
    Verify Name Negative  ${name}  ?
    Verify Name Negative  ${name}  :
    Verify Name Negative  ${name}  "
    Verify Name Negative  ${name}  {
    Verify Name Negative  ${name}  }
    Verify Name Negative  ${name}  |

Various Suite Setup
	Base Suite Setup
	Cli Copy  running-config  snapshot://startup-config
	Cli Copy  running-config  snapshot://startup-config  node=slave

Various Suite Teardown
	Base Suite Teardown

Setup
	Base Test Setup
    Cli Run  master  enable
    Cli Run  master  config

Teardown
	Cli Run  master  exit
	Cli Run  master  enable
	Cli Run  master  config
	Run Keyword and Ignore Error  Cli Run  master  reauth admin adminadmin
	Run Keyword and Ignore Error  Cli  master  enable; config; no aaa accounting exec default start-stop group tacacs+  timeout=200
	Cli Copy  snapshot://startup-config  running-config
	Base Test Teardown
